# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Ray Donnelly <mingw.android@gmail.com>

pkgbase=msys2-runtime
pkgname=('msys2-runtime' 'msys2-runtime-devel')
pkgver=3.4.7
pkgrel=1
pkgdesc="Cygwin POSIX emulation engine"
arch=('x86_64')
url="https://www.cygwin.com/"
license=('GPL')
groups=('base')
makedepends=('cocom'
             'texinfo'
             'git'
             'perl'
             'gcc'
             'make'
             'mingw-w64-cross-crt-git'
             'mingw-w64-cross-gcc'
             'mingw-w64-cross-zlib'
             'zlib-devel'
             'gettext-devel'
             'libiconv-devel'
             'autotools'
             'xmlto'
             'docbook-xsl')
source=('msys2-runtime'::git+https://github.com/cygwin/cygwin#tag=cygwin-${pkgver}
        msys2-runtime.commit
        0001-Add-MSYS2-triplet.patch
        0002-Fix-msys-library-name-in-import-libraries.patch
        0003-Rename-dll-from-cygwin-to-msys.patch
        0004-Add-functionality-for-converting-UNIX-paths-in-argum.patch
        0005-Add-functionality-for-changing-OS-name-via-MSYSTEM-e.patch
        0006-Move-root-to-usr.-Change-sorting-mount-points.-By-de.patch
        0007-Instead-of-creating-Cygwin-symlinks-use-deep-copy-by.patch
        0008-Automatically-rewrite-TERM-msys-to-TERM-cygwin.patch
        0009-Do-not-convert-environment-for-strace.patch
        0010-path_conv-special-case-root-directory-to-have-traili.patch
        0011-dcrt0.cc-Untangle-allow_glob-from-winshell.patch
        0012-dcrt0.cc-globify-Don-t-quote-literal-strings-differe.patch
        0013-strace.cc-Don-t-set-MSYS-noglob.patch
        0014-Add-debugging-for-build_argv.patch
        0015-Add-debugging-for-strace-make_command_line.patch
        0016-environ.cc-New-facility-environment-variable-MSYS2_E.patch
        0017-Fix-native-symbolic-link-spawn-passing-wrong-arg0.patch
        0018-strace-quiet-be-really-quiet.patch
        0019-Introduce-the-enable_pcon-value-for-MSYS.patch
        0020-popen-call-usr-bin-sh-instead-of-bin-sh.patch
        0021-Expose-full-command-lines-to-other-Win32-processes-b.patch
        0022-Do-not-show-Error-dialogs-by-default.patch
        0023-Add-a-helper-to-obtain-a-function-s-address-in-kerne.patch
        0024-Emulate-GenerateConsoleCtrlEvent-upon-Ctrl-C.patch
        0025-kill-kill-Win32-processes-more-gently.patch
        0026-Cygwin-make-option-for-native-inner-link-handling.patch
        0027-docs-skip-building-texinfo-and-PDF-files.patch
        0028-install-libs-depend-on-the-toollibs.patch
        0029-POSIX-ify-the-SHELL-variable.patch
        0030-Handle-ORIGINAL_PATH-just-like-PATH.patch
        0031-uname-allow-setting-the-system-name-to-CYGWIN.patch
        0032-Pass-environment-variables-with-empty-values.patch
        0033-Optionally-disallow-empty-environment-values-again.patch
        0034-build_env-respect-the-MSYS-environment-variable.patch
        0035-When-converting-to-a-Unix-path-avoid-double-trailing.patch
        0036-msys2_path_conv-pass-PC_NOFULL-to-path_conv.patch
        0037-Revert-Cygwin-Enable-dynamicbase-on-the-Cygwin-DLL-b.patch
        0038-Stop-assuming-that-there-are-no-spaces-in-POSIX-styl.patch
        0039-Handle-8-bit-characters-under-LOCALE-C.patch
        0040-Mention-the-extremely-useful-small_printf-function.patch
        0041-Leave-arguments-starting-with-a-tilde-or-quote-alone.patch
        0042-Leave-Git-s-name-and-message-arguments-alone-please.patch
        0043-Leave-paths-containing-any-special-characters-alone.patch
        0044-Leave-paths-containing-alone.patch
        0045-Skip-posix-to-windows-conversion-when-is-seen.patch
        0046-Also-leave-Git-s-rev-.-name-syntax-alone.patch
        0047-Arguments-starting-with-are-no-paths.patch
        0048-Prevent-scp-style-arguments-from-being-mangled.patch
        0049-Fixed-path-converting-with-non-ascii-char.patch
        0050-path-conversion-Introduce-ability-to-switch-off-conv.patch
        0051-Allow-overriding-the-home-directory-via-the-HOME-var.patch
        0052-Respect-db_home-setting-even-for-the-SYSTEM-account.patch
        0053-Respect-the-db_home-env-setting-under-more-circumsta.patch
        0054-Allow-native-symlinks-to-non-existing-targets-in-nat.patch
        0055-Make-paths-WCS-MBS-conversion-explicit.patch
        0056-Use-MB_CUR_MAX-6-by-default.patch
        0057-Change-the-default-base-address-for-x86_64.patch
        0058-Avoid-sharing-cygheaps-across-Cygwin-versions.patch
        0059-uname-report-msys2-runtime-commit-hash-too.patch
        0060-Revert-Cygwin-pipe-Restore-blocking-mode-for-cygwin-.patch)
sha256sums=('SKIP'
            'eeb2edd7d3695e4a5b149a99e4bdd48de115ea48a3def1bbbec60a431f13263d'
            'c092f44f33e526c93282de5f1b12628665b38d607c2e7628d8437634ce9d133f'
            'a19b83098b577f64b7df42197ada140b58601f0acb20feba304f4dcdce8563c7'
            '8c71c0477544aec58d4c213c59e273f6faf94fd8691248276af2f77ffec1b481'
            'fa5a2ee77adf6cc8b5ee81bc11167398692d13a75bd8bdf0b0cba9c008993511'
            '926c774aefc9d8ea6f6636a4598fe9231be984f1d8a2f09b55c7b7d2946d2f55'
            '542e64ffdc55b072bed8f7d1694887b0a181fe84192abf3a901ff1a4ad5b1752'
            '290e61c5179da84b89c9b70f7d923ad86c953a5151eccd385af13b2d1156d972'
            '37170be2f5b33df9bcafd4d0695329502e3431dde3c46f624aa5b77fb355daf9'
            '47d7e54e0bc4d80fb9e1503ffb53491cb4ce14330d556144c4f55f5237e8da5a'
            'ec0af91632f95dd29a4fe4d1afe305674c771ec961028471f9fca856d318e04f'
            '2ae67271e5b665f206c6893208ef1a61d644d8cd383046df486650d7d7431ad1'
            'd398703e502195a3bd83dd2f091617c33169f3580744577ed2355246dcdd780e'
            '699d6177a730db9be8e7248e4ca4a549e23b5283fc8c3da6c9be90bc69dbe6c9'
            '2a2abd73f7e74a2989a550a92768afe05bd84d847cbe818e60be59d8c13f411c'
            '30d2883597f8ac303a91a4f1186bcf95acdf41c0e54c07a011e91e3ff0abe9b5'
            '367d83303ebdc4a75e0488e6a50a90851a74a15dbeacd9aad07d2ec638ae5cc3'
            '48ce209efb557058a12daedd81dbe1511c43e24e00a92a16175b7c7e975a4aa8'
            '6bd65ec59d9c22df2d8cd5c1d82139296ac075ce8d6fe498543d8b3da776e707'
            '3b19e1f8913dbbd1833e35335de26b04bf48ea1de41d5a050a660a888b79a43b'
            '64b71b31274a2084b486158fa4cb9db985a211d546adb090ab7135618540d393'
            '92736470f5d75da1054a253ee830e6567705f15ddf9d38f0565616fa4455fa77'
            '892ed7b94670f23ae3e3268b4ea8d3f6d9589e11086efa11faa6a6cf1caade37'
            '8db2dd13e0729c7b15c4b9a85e2cc2ef1a164a864ac3b8ef84e4d62805870308'
            '89559ba6c6a23efe80488002b3447cc90e1ad8519c7949af4372c8fb0e61b269'
            'a7ff1f03aa39addc44270ce2ed0f1a085f3c807dc12b520f68df572f7b8d77b9'
            '0497442a0a30becddc14597a2a57e4b428ea765ae99e1c9bb949eea6c01d0fce'
            '342766f97fde03afd0630c29f386559fdc827822dd87d1ecfecd2858e538df10'
            '9ece8af6ee86b04018fc4557479928766bffd50c2fd96052dc0cfc71445be0fe'
            '0d9478225cd6f38ad36f86b3c3d52d76c10c20f82cfd006cfd9938c7beb8c718'
            '606f3905b454a490535b0f70b8d1623f55168cff6d28604b788f255c2d70e1f1'
            'f44db73b621a115ea9cb5bf4eaf1afc45c910e70c0534670fc7c81475d396c87'
            '69a251b56ff2d16127450ee16aff43d025d5ea80a57d910baedd15239ef4c92b'
            'ffa048a45e698052e890c62ee3c4128070b7d1013dcee8e127404d9247fe7ff3'
            'f0ef04bee4d6bb41d815e60ae14bf0801e886c01fd3e1fef01cdc755fdf88eef'
            'c1bb2a92ee6f96be1467042a3d2203cd4bd196388a12331c952f5112e2771f2e'
            'aeaa2b4a0a05da5f1a6acf2ed87c00e514a4a96a7850096e9e14ed31b57b7737'
            'abc27017d8c750f971a96df5d00f494bbfffe46258b5635ffc2118b9524c897c'
            'a2f11abdc21acc7c0c98f048ea5cb99d33c6a0f232f920f5765bac6a3d86295b'
            '09d5e3e4cb100649b9db0bb8669bd3594644541cec215bb83aee8810b911248c'
            '9e0a3aab4f2c223a3b48d429ebde02b2b948d43dff9beec9b179ecde8164a5e3'
            'c324a53f37f44c992f56cc21983df0db1d5b71800007daa3d3adc4dc3497f9e5'
            'd1b8aacabaa17b25b1fa54103dd154b041c608f210c4416d4ad0c0d3546dcee5'
            '5009812263ab218fc2bbfca706646bf16b7e37f93c7d7524fe6aea55b0260692'
            'f5dcd40e3ac66eb8ac173870e75c58329ddc4ad43b95bfe075fe8f174497d346'
            '9b14e9e1de3bf78b3994d96d26922e874b25720895da7a6d8740250770e8c12e'
            'f4127347c171aef370425ea8d8fbc0730d808a6f799735f29538f3923575cadf'
            '3f6857f3f5afaea4657dbb7a43f3edb7cc12ed55e2b2a027fb4cbd411469d30b'
            '13b54fe2c59ac21357a0523a0d8c0435f5f46feade44b0558ce7b1432ef8263c'
            'baf4ecd09be0b81628c5b502924413ff26fa44ca1a2c31870d7b8118813bf13b'
            'aa75101933a4a6c7c39d9c5bc4ca6dc7a3d553f78bceb309ca15704f25e70604'
            '461f9c2450eb7296583155c802818000b07c04f1f1e2d38b29774928d3859e06'
            '4bc9bfadc019a1ac3ab57cb4fef9b47f61ae519e882faa4812d78e57b32d85c0'
            'cb661522ca67e27a45983534b9f87e24dd2595e937a475c3a1f9e570e0f24e2c'
            '3067f00729bcd86c6a984e8bb1846b13e5f078f98b00e10345ba630f3c1350bc'
            '09dd3eaba5e89fca7c3dee851e7732e2530b14f8a036e409bd8e46bda8c07ed1'
            '0eb051afba4cc9ac637941d2011dbbf8e9f66c2f26f2d73eb6ac4584b6f0f965'
            '8f2a61c291fc8e0b864892f13f24454d52733ac949b56cd8d62bf7cfc1aea957'
            '0557eba646e49091dbcc5c24600544be57d053d2e979219062c6cf8e988de6ec'
            'b1cc58ff353fa74bca23359c62687a19cf9067b16c123ecfe016496a7d276b4b'
            '1f9a2cfd1ad42e3b101028d10555a8fe4782ba2b0df931d8e23784d2c76df6ad')

# Helper macros to help make tasks easier #
apply_patch_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying $_patch"
    patch -Nbp1 -i "${srcdir}/${_patch}"
  done
}

apply_git_am_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying $_patch"
    git apply "${srcdir}/${_patch}"
  done
}

del_file_exists() {
  for _fname in "$@"
  do
    if [ -f $_fname ]; then
      rm -rf $_fname
    fi
  done
}
# =========================================== #

prepare() {
  cd "${srcdir}"/msys2-runtime
  if test true != "$(git config core.symlinks)"
  then
    git config core.symlinks true &&
    /usr/bin/git reset --hard
  fi
  if test false != "$(git config core.autocrlf)"
  then
    git config core.autocrlf false &&
    rm "$(git rev-parse --git-path index)" &&
    /usr/bin/git reset --hard
  fi
  del_file_exists winsup/cygwin/msys2_path_conv.cc \
    winsup/cygwin/msys2_path_conv.h \
    winsup/cygwin/include/cygwin/exit_process.h
  apply_git_am_with_msg \
 \
  0001-Add-MSYS2-triplet.patch \
  0002-Fix-msys-library-name-in-import-libraries.patch \
  0003-Rename-dll-from-cygwin-to-msys.patch \
  0004-Add-functionality-for-converting-UNIX-paths-in-argum.patch \
  0005-Add-functionality-for-changing-OS-name-via-MSYSTEM-e.patch \
  0006-Move-root-to-usr.-Change-sorting-mount-points.-By-de.patch \
  0007-Instead-of-creating-Cygwin-symlinks-use-deep-copy-by.patch \
  0008-Automatically-rewrite-TERM-msys-to-TERM-cygwin.patch \
  0009-Do-not-convert-environment-for-strace.patch \
  0010-path_conv-special-case-root-directory-to-have-traili.patch \
  0011-dcrt0.cc-Untangle-allow_glob-from-winshell.patch \
  0012-dcrt0.cc-globify-Don-t-quote-literal-strings-differe.patch \
  0013-strace.cc-Don-t-set-MSYS-noglob.patch \
  0014-Add-debugging-for-build_argv.patch \
  0015-Add-debugging-for-strace-make_command_line.patch \
  0016-environ.cc-New-facility-environment-variable-MSYS2_E.patch \
  0017-Fix-native-symbolic-link-spawn-passing-wrong-arg0.patch \
  0018-strace-quiet-be-really-quiet.patch \
  0019-Introduce-the-enable_pcon-value-for-MSYS.patch \
  0020-popen-call-usr-bin-sh-instead-of-bin-sh.patch \
  0021-Expose-full-command-lines-to-other-Win32-processes-b.patch \
  0022-Do-not-show-Error-dialogs-by-default.patch \
  0023-Add-a-helper-to-obtain-a-function-s-address-in-kerne.patch \
  0024-Emulate-GenerateConsoleCtrlEvent-upon-Ctrl-C.patch \
  0025-kill-kill-Win32-processes-more-gently.patch \
  0026-Cygwin-make-option-for-native-inner-link-handling.patch \
  0027-docs-skip-building-texinfo-and-PDF-files.patch \
  0028-install-libs-depend-on-the-toollibs.patch \
  0029-POSIX-ify-the-SHELL-variable.patch \
  0030-Handle-ORIGINAL_PATH-just-like-PATH.patch \
  0031-uname-allow-setting-the-system-name-to-CYGWIN.patch \
  0032-Pass-environment-variables-with-empty-values.patch \
  0033-Optionally-disallow-empty-environment-values-again.patch \
  0034-build_env-respect-the-MSYS-environment-variable.patch \
  0035-When-converting-to-a-Unix-path-avoid-double-trailing.patch \
  0036-msys2_path_conv-pass-PC_NOFULL-to-path_conv.patch \
  0037-Revert-Cygwin-Enable-dynamicbase-on-the-Cygwin-DLL-b.patch \
  0038-Stop-assuming-that-there-are-no-spaces-in-POSIX-styl.patch \
  0039-Handle-8-bit-characters-under-LOCALE-C.patch \
  0040-Mention-the-extremely-useful-small_printf-function.patch \
  0041-Leave-arguments-starting-with-a-tilde-or-quote-alone.patch \
  0042-Leave-Git-s-name-and-message-arguments-alone-please.patch \
  0043-Leave-paths-containing-any-special-characters-alone.patch \
  0044-Leave-paths-containing-alone.patch \
  0045-Skip-posix-to-windows-conversion-when-is-seen.patch \
  0046-Also-leave-Git-s-rev-.-name-syntax-alone.patch \
  0047-Arguments-starting-with-are-no-paths.patch \
  0048-Prevent-scp-style-arguments-from-being-mangled.patch \
  0049-Fixed-path-converting-with-non-ascii-char.patch \
  0050-path-conversion-Introduce-ability-to-switch-off-conv.patch \
  0051-Allow-overriding-the-home-directory-via-the-HOME-var.patch \
  0052-Respect-db_home-setting-even-for-the-SYSTEM-account.patch \
  0053-Respect-the-db_home-env-setting-under-more-circumsta.patch \
  0054-Allow-native-symlinks-to-non-existing-targets-in-nat.patch \
  0055-Make-paths-WCS-MBS-conversion-explicit.patch \
  0056-Use-MB_CUR_MAX-6-by-default.patch \
  0057-Change-the-default-base-address-for-x86_64.patch \
  0058-Avoid-sharing-cygheaps-across-Cygwin-versions.patch \
  0059-uname-report-msys2-runtime-commit-hash-too.patch \
  0060-Revert-Cygwin-pipe-Restore-blocking-mode-for-cygwin-.patch
}

build() {
  [[ -d "${srcdir}"/build-${CHOST} ]] && rm -rf "${srcdir}"/build-${CHOST}
  mkdir -p "${srcdir}"/build-${CHOST} && cd "${srcdir}"/build-${CHOST}

  # Gives more verbose compile output when debugging.
  local -a extra_config
  if check_option "debug" "y"; then
    export CCWRAP_VERBOSE=1
    OPTIM="-O0"
    extra_config+=(--enable-debugging)
  else
    OPTIM="-O2"
  fi

  CFLAGS="$OPTIM -pipe -g0 -Wno-error=deprecated -Wno-error=stringop-truncation -Wno-error=missing-attributes -Wno-error=maybe-uninitialized" #-Wno-error=class-memaccess
  CXXFLAGS="$OPTIM -pipe -g0 -Wno-error=deprecated -Wno-error=stringop-truncation -Wno-error=missing-attributes -Wno-error=maybe-uninitialized" #-Wno-error=class-memaccess

  # otherwise it asks git which appends "-dirty" because of our uncommited patches
  CFLAGS+=" -DCYGPORT_RELEASE_INFO=${pkgver}"

  (cd "${srcdir}/msys2-runtime/winsup" && ./autogen.sh)

  "${srcdir}"/msys2-runtime/configure \
    --with-msys2-runtime-commit="$(cat "${srcdir}/msys2-runtime.commit")" \
    --prefix=/usr \
    --build=${CHOST} \
    --sysconfdir=/etc \
    "${extra_config[@]}"
  LC_ALL=C make

  if test -n "$SIGNTOOL"
  then
    eval "$SIGNTOOL" \
      ${PWD}/${CHOST}/winsup/cygwin/new-msys-2.0.dll \
      ${PWD}/${CHOST}/winsup/utils/*.exe ${PWD}/${CHOST}/winsup/utils/mingw/*.exe
  fi

  LC_ALL=C make -j1 DESTDIR="${srcdir}"/dest install

  #pushd ${CHOST}/winsup/cygwin > /dev/null
  #LANG=C make libmsys2_s.a
  #cp libmsys2_s.a "${srcdir}"/dest/usr/${CHOST}/lib/
  #popd > /dev/null

  rm -rf "${srcdir}"/dest/etc
}

package_msys2-runtime() {
  pkgdesc="Posix emulation engine for Windows"
  groups=('base')
  options=('!strip')
  conflicts=('catgets' 'libcatgets' 'msys2-runtime-3.4')
  replaces=('catgets' 'libcatgets' 'msys2-runtime-3.4')

  mkdir -p "${pkgdir}"/usr
  cp -rf "${srcdir}"/dest/usr/bin "${pkgdir}"/usr/
  cp -rf "${srcdir}"/dest/usr/libexec "${pkgdir}"/usr/
  rm -f "${pkgdir}"/usr/bin/cyglsa-config
  rm -f "${pkgdir}"/usr/bin/cyglsa.dll
  rm -f "${pkgdir}"/usr/bin/cyglsa64.dll
  rm -f "${pkgdir}"/usr/bin/cygserver-config
  cp -rf "${srcdir}"/dest/usr/share "${pkgdir}"/usr/
}

package_msys2-runtime-devel() {
  pkgdesc="MSYS2 headers and libraries"
  groups=('msys2-devel')
  depends=("msys2-runtime=${pkgver}")
  conflicts=('libcatgets-devel' 'msys2-runtime-3.4-devel')
  replaces=('libcatgets-devel' 'msys2-runtime-3.4-devel')

  mkdir -p "${pkgdir}"/usr/bin
  cp -rLf "${srcdir}"/dest/usr/${CHOST}/include "${pkgdir}"/usr/
  rm -f "${pkgdir}"/usr/include/iconv.h
  rm -f "${pkgdir}"/usr/include/unctrl.h
  # provided by libtirpc
  rm -fr "${pkgdir}"/usr/include/rpc/

  cp -rLf "${srcdir}"/dest/usr/${CHOST}/lib "${pkgdir}"/usr/
}

# return 0
# To hack on this:
# cd /c/repo-MSYS2/msys2-runtime/
# pushd src/build-i686-pc-msys
# LANG=C make && make -j1 DESTDIR=/c/repo-MSYS2/msys2-runtime/src/dest install
# popd
# makepkg -sRLf
# pacman -U msys2-runtime*.xz

# Quicker:
# open cmd.exe
# set "PATH=C:\\msys32\\usr\\bin;%PATH%"
# E:
# pushd E:\m2\repo-MSYS2\msys2-runtime\src\build-i686-pc-msys\i686-pc-msys\winsup\cygwin
# C:/msys32/usr/bin/bash -c "LANG=C && make"
# copy /y new-msys-2.0.dll C:\msys32\usr\bin\msys-2.0.dll
# popd
# C:
# C:/msys32/usr/bin/strace ls / > C:/strace.txt 2>&1
#
