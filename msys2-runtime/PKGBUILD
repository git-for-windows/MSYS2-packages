# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Ray Donnelly <mingw.android@gmail.com>

pkgbase=msys2-runtime
pkgname=('msys2-runtime' 'msys2-runtime-devel')
pkgver=3.3.6
pkgrel=1
pkgdesc="Cygwin POSIX emulation engine"
arch=('i686' 'x86_64')
url="https://www.cygwin.com/"
license=('GPL')
groups=('base')
makedepends=('cocom'
             'texinfo'
             'git'
             'perl'
             'gcc'
             'make'
             'mingw-w64-cross-crt-git'
             'mingw-w64-cross-gcc'
             'mingw-w64-cross-zlib'
             'zlib-devel'
             'gettext-devel'
             'libiconv-devel'
             'diffutils')
# options=('debug' '!strip')
source=('msys2-runtime'::git+https://github.com/cygwin/cygwin#tag=cygwin-${pkgver//./_}-release
        0001-Add-MSYS2-triplet.patch
        0002-Fix-msys-library-name-in-import-libraries.patch
        0003-Rename-dll-from-cygwin-to-msys.patch
        0004-Add-functionality-for-converting-UNIX-paths-in-argum.patch
        0005-Add-functionality-for-changing-OS-name-via-MSYSTEM-e.patch
        0006-Move-root-to-usr.-Change-sorting-mount-points.-By-de.patch
        0007-Instead-of-creating-Cygwin-symlinks-use-deep-copy-by.patch
        0008-Automatically-rewrite-TERM-msys-to-TERM-cygwin-With-.patch
        0009-Do-not-convert-environment-for-strace.patch
        0010-Special-case-for-converting-root-directory-to-have-t.patch
        0011-dcrt0.cc-Untangle-allow_glob-from-winshell.patch
        0012-dcrt0.cc-globify-Don-t-quote-literal-strings-differe.patch
        0013-strace.cc-Don-t-set-MSYS-noglob.patch
        0014-Add-debugging-for-build_argv.patch
        0015-Add-debugging-for-strace-make_command_line.patch
        0016-environ.cc-New-facility-environment-variable-MSYS2_E.patch
        0017-Fix-native-symbolic-link-spawn-passing-wrong-arg0.patch
        0018-QueryUnbiasedInterruptTime-must-be-load-from-kernel3.patch
        0019-strace-quiet-be-really-quiet.patch
        0020-Default-to-disable_pcon.patch
        0021-Introduce-the-enable_pcon-value-for-MSYS.patch
        0022-popen-call-usr-bin-sh-instead-of-bin-sh.patch
        0023-CI-add-a-GHA-for-doing-a-basic-build-test.patch
        0024-Set-up-a-GitHub-Action-to-keep-in-sync-with-Cygwin.patch
        0025-Expose-full-command-lines-to-other-Win32-processes-b.patch
        0026-Disable-the-cygwin-GitHub-workflow.patch
        0027-Do-not-show-Error-dialogs-by-default.patch
        0028-Add-a-helper-to-obtain-a-function-s-address-in-kerne.patch
        0029-Emulate-GenerateConsoleCtrlEvent-upon-Ctrl-C.patch
        0030-kill-kill-Win32-processes-more-gently.patch
        0031-Cygwin-make-option-for-native-inner-link-handling.patch
        0032-docs-skip-building-texinfo-and-PDF-files.patch
        0033-install-libs-depend-on-the-toollibs.patch
        0034-POSIX-ify-the-SHELL-variable.patch
        0035-Handle-ORIGINAL_PATH-just-like-PATH.patch
        0036-uname-allow-setting-the-system-name-to-CYGWIN.patch
        0037-Implicitly-support-the-dev-fd-symlink-and-friends.patch
        0038-Fix-incorrect-path-conversion-trailing-slash.patch
        0039-Pass-environment-variables-with-empty-values.patch
        0040-Handle-8-bit-characters-under-LOCALE-C.patch
        0041-Mention-the-extremely-useful-small_printf-function.patch
        0042-Add-a-helpful-debug-message-for-posix-to-windows-con.patch
        0043-Stop-assuming-that-there-are-no-spaces-in-POSIX-styl.patch
        0044-Avoid-unnecessary-recursion-in-find_path_start_and_t.patch
        0045-Leave-arguments-starting-with-a-tilde-or-quote-alone.patch
        0046-Leave-Git-s-name-and-message-arguments-alone-please.patch
        0047-Leave-paths-containing-any-special-characters-alone.patch
        0048-Leave-paths-containing-alone.patch
        0049-Skip-posix-to-windows-conversion-when-is-seen.patch
        0050-Also-leave-Git-s-rev-.-name-syntax-alone.patch
        0051-Arguments-starting-with-are-no-paths.patch
        0052-Prevent-scp-style-arguments-from-being-mangled.patch
        0053-Fixed-path-converting-with-non-ascii-char.patch
        0054-path-conversion-Introduce-ability-to-switch-off-conv.patch
        0055-Allow-overriding-the-home-directory-via-the-HOME-var.patch
        0056-Respect-db_home-setting-even-for-the-SYSTEM-account.patch
        0057-Respect-the-db_home-env-setting-under-more-circumsta.patch
        0058-Allow-native-symlinks-to-non-existing-targets-in-nat.patch
        0059-Make-paths-WCS-MBS-conversion-explicit.patch
        0060-Use-MB_CUR_MAX-6-by-default.patch
        0061-Change-the-default-base-address-for-x86_64.patch
        0062-Do-not-try-to-sync-with-Cygwin.patch
        0063-Avoid-sharing-cygheaps-across-Cygwin-versions.patch
        0064-dumper-avoid-linker-problem-when-libbfd-depends-on-l.patch)
sha256sums=('SKIP'
            '0113ee0d3bad78a0575b83e321d41d859b99140d9471762c8a82e766cae36c48'
            'ee1c4c5161eb977c48d02678069fb1836ce36a46b626a71b30ae3bc0fabf77c6'
            'bea035aad349357ea7e50cf4f98a4100c68cba97862f7ff1a04a4330982e854a'
            'd2a7492e80b16d63cdf4819b92596ff8da07205fd4a71af9165566eeb7dcc025'
            '77a17d06e2ee87fc291a982fa9716ea41f03f149ebb4f64cdfb539cb22d26bfd'
            '0c05da08b85860c4bdf2e3af887b9fff2bcc76a676920e3f6103f658ecce4843'
            '08168ff2bba074b61e2a2abb4df7d2dc075c76d8abe2366d9a788addda936212'
            '73d6d0e5db7493e518f1ad48620284ccc95da060a50866809479e2fef8806063'
            '9059846ceff22e04e19b70e2f26ba400c9c407df98558e3d0e3288b8eb82e33c'
            '04a6fe5a3683c443751b6d7994efff7e864a546d848b7468ce56685acdd10c4d'
            'b2301689e8480132d8cfc2d358d840a3abf62c739326b0f861c027238b2b4793'
            '2d50f2240bb06403b1d132e7ec1c2dc0a13b8eab0b5d86efedb6b89925ca5d78'
            'b3eadbea5a5b436da4e71490b96ef13248bad5e4eeabfa2fe4089d7bd5d32a4e'
            '0bc9f696d74fccee639e1e755e3c22386d55c41aeb1d9a39937e37b96a825b0e'
            '46c80536fc8907c9d4980a415efa750da44ecbdaebc89d37e23235cf398275c7'
            '95a3d6afacbec7f539a81fa1aedd8caf1bca3e6662722a3a5294fe50a385d562'
            '24556d9561d0fb8f54696d01953c7ac09c7857d0f6fc22c8f1bba89a03a47dd3'
            'c770804225ef066e372dfe650213a74457a5878f6d77c9e935aa2ba2f2c9f8c2'
            '5e55f5f42a7f6eae1a6c174fcf595ad320fbf10f2e6403fdee5b1e81aad92cc2'
            'b93b7aa87954c26cc2bf130d7b7a1b972681328d06e50c3cd19169026453e8a2'
            '0c22feb472008a3de5bacb01f71ce5ec6a463fb3f73a8c14721805b747d919be'
            'e6a37015f721ccf5ed34025ab14a88677e712b6a99c83f6a62b097c2aebc2b27'
            'c2fa4772423ddb45f83b39ca86dfce48038273560905540143c13d1110fe3a7f'
            'fbbad7015b7e3fbe3822bc46ff49d8ab2435a4248c95014d1900c25a696118ed'
            'a74e55261d16456326159c35527979d069cf177fa32dad45057c42ce6ffef5b5'
            '30c3ff367fd486d64753e16e765349c415ac5e494080561fe315265412ebc54a'
            '4910fc461ce55caed9a0a6f4b903c76a4463353019f309afe2bd7c8ad8479cb3'
            '55628c67d2a620a1c0d83538ea680b3695ab356d07ebe305530828e4052bf6f5'
            'a89ffa7f8f9352edcb57e3e3c95ba0e7970bda15949c613e0912f9ec4d19a0b3'
            'e2044589ac30ffb305b35bc6fceccf958bc8681f481372d04d8ba76e663360b4'
            'a9c86cae1aaef9587e17900e8199b0f59fef2e8a019e609727e9b58db8ae4ecf'
            '95f016925dfe869ac6a0138921c65feb1e8059d1805cc211783d16b9175a13f9'
            '4dd388e7b6cd8b0a48e9853dfae7d405602c29caa781f937a79817bdaf885fe0'
            '0e83f3e466990f417a6bff57524675a7871edb45ab8f4a3799ef42a2a51f47dc'
            '0030bde788e681c86fa0db5a4dbab8aed25ef94f0b3b3eab7eb99e44e8658749'
            '45baa93e5b56a4fed68ec6a5c150b016ba577815752f3300f96a4f123294c65c'
            'b75248bad8fae68d0f43e8926f58bc2bfbda1e4b493fd2fff9369a81066439bd'
            'f4845cecffd6898d71a3e5cac4fd84bbc0b817376756049475fbfb6d9617c2e9'
            'd67416ef4cd9dc9c7fe9d90b063360fee63fdd65b87db22f8d9b3b8a1cbb4ec3'
            'eaf96cb4e0960574be51abda9d0999c0ba8189a4e68eb7e3f3390b60bca3fded'
            'c62da0e2ec324ad684484a524673c687804b8f70312be7fcb41d13c927a409cb'
            'e9217f71bd9258d4f618b0eaa4f6a5c1f7b3d4cab55a287f6406b097c6ea7d75'
            'aff4ed2f32e8f9e1249519e57602c08d4f10a71e97cc01c2b05691da9d9c2646'
            '7497dd07842dea6e072bbf355bea4c1e923cdc6456bd3d1a2761ea3a231df3ae'
            '829948d3d0bd29dfdef23af3e6a2491ba641b3a12b630b4b3586a2bb281ba2d2'
            '476e697543e1a83c2dfe0904a95669952ec5e39bb169eca64c5afef084274bdd'
            'ff137e48462e77fb0e8013ba569edcc33b7384fc00825f8adc35d1d8e8d889df'
            'd3a2ec700fcfaaf627fca229c5e6e0a6dc812d4a1a7df7f3c49daeb5dcf0e550'
            '63864011473a03f9b2593a3521a235e3e05ee2c90c826b21375bea4ed771468b'
            '4229b0460d7eaff092e490fec7d80d16da1d96565cd66ab0d40e43c55e083ce9'
            '2e2ddb48b5e0a211f417013dd44134e1171663b80ce68c0d134fd95eabbd4250'
            '53d7f1580bb160badc350ca4c7885e88e55fc765bc2f4901120583475222debf'
            '910b4f73bda4185fef6340dda48ebb0a03fa50b28d46fb302bd1c955157ae04d'
            '6cb47aab3ec690feb3e4c4078b00d732caaeee75f6468c8d7afb44f2dbbdb9d8'
            '20f5e7b17a8c7200e54d5877507001d1eedc02d06a1305f9b57b3a085130cee4'
            '49f5c4be2f022111a230923f8e7da8b9478c39cdc6dd35c796bc19b88472a724'
            '1b79eba03761a20fb5ee6fe8f4c944bcecae1e760aa25f76d4d072d1047cf93b'
            'cc8c48cc56e3be4fbdd860ccacd83a1efdae991461e94534817681e917d0355f'
            'cba2181936051214a7218ee933cc9f3619e46b500fdaadfa69b7f38eefda3f10'
            '4809e399aead149f5106248196249130537de492d43e4ba95bfeded84305f39b'
            '5188dc6b44c58eb0140c9078f7bbdb999a228755ef449fa055f8c3415ed2baba'
            '30f5b96b1fb1c22d0dd463d2ebd592d72650b20db4bc5fbf7dd7c2d81f6c29ce'
            '9559bbb33faf6773cd6439be05962a116184b945b909ba49049db3b7c3761a11'
            'cd576bc86766e5df42af3ac5594073859e562e24ef01e7ab21f0d38ef4d87834')

# Helper macro to help make tasks easier #
del_file_exists() {
  for _fname in "$@"
  do
    if [ -f $_fname ]; then
      rm -rf $_fname
    fi
  done
}

prepare() {
  cd "${srcdir}"/msys2-runtime
  if test true != "$(git config core.symlinks)"
  then
    git config core.symlinks true &&
    /usr/bin/git reset --hard
  fi
  if test false != "$(git config core.autocrlf)"
  then
    git config core.autocrlf false &&
    rm "$(git rev-parse --git-path index)" &&
    /usr/bin/git reset --hard
  fi
  del_file_exists winsup/cygwin/msys2_path_conv.cc \
    winsup/cygwin/msys2_path_conv.h
  git am --committer-date-is-author-date --ignore-space-change "${srcdir}"/0001-Add-MSYS2-triplet.patch
  git am --committer-date-is-author-date --ignore-space-change "${srcdir}"/0002-Fix-msys-library-name-in-import-libraries.patch
  git am --committer-date-is-author-date --ignore-space-change "${srcdir}"/0003-Rename-dll-from-cygwin-to-msys.patch
  git am --committer-date-is-author-date --ignore-space-change "${srcdir}"/0004-Add-functionality-for-converting-UNIX-paths-in-argum.patch
  git am --committer-date-is-author-date --ignore-space-change "${srcdir}"/0005-Add-functionality-for-changing-OS-name-via-MSYSTEM-e.patch
  git am --committer-date-is-author-date --ignore-space-change "${srcdir}"/0006-Move-root-to-usr.-Change-sorting-mount-points.-By-de.patch
  git am --committer-date-is-author-date --ignore-space-change "${srcdir}"/0007-Instead-of-creating-Cygwin-symlinks-use-deep-copy-by.patch
  git am --committer-date-is-author-date --ignore-space-change "${srcdir}"/0008-Automatically-rewrite-TERM-msys-to-TERM-cygwin-With-.patch
  git am --committer-date-is-author-date --ignore-space-change "${srcdir}"/0009-Do-not-convert-environment-for-strace.patch
  git am --committer-date-is-author-date --ignore-space-change "${srcdir}"/0010-Special-case-for-converting-root-directory-to-have-t.patch
  git am --committer-date-is-author-date --ignore-space-change "${srcdir}"/0011-dcrt0.cc-Untangle-allow_glob-from-winshell.patch
  git am --committer-date-is-author-date --ignore-space-change "${srcdir}"/0012-dcrt0.cc-globify-Don-t-quote-literal-strings-differe.patch
  git am --committer-date-is-author-date --ignore-space-change "${srcdir}"/0013-strace.cc-Don-t-set-MSYS-noglob.patch
  git am --committer-date-is-author-date --ignore-space-change "${srcdir}"/0014-Add-debugging-for-build_argv.patch
  git am --committer-date-is-author-date --ignore-space-change "${srcdir}"/0015-Add-debugging-for-strace-make_command_line.patch
  git am --committer-date-is-author-date --ignore-space-change "${srcdir}"/0016-environ.cc-New-facility-environment-variable-MSYS2_E.patch
  git am --committer-date-is-author-date --ignore-space-change "${srcdir}"/0017-Fix-native-symbolic-link-spawn-passing-wrong-arg0.patch
  git am --committer-date-is-author-date --ignore-space-change "${srcdir}"/0018-QueryUnbiasedInterruptTime-must-be-load-from-kernel3.patch
  git am --committer-date-is-author-date --ignore-space-change "${srcdir}"/0019-strace-quiet-be-really-quiet.patch
  git am --committer-date-is-author-date --ignore-space-change "${srcdir}"/0020-Default-to-disable_pcon.patch
  git am --committer-date-is-author-date --ignore-space-change "${srcdir}"/0021-Introduce-the-enable_pcon-value-for-MSYS.patch
  git am --committer-date-is-author-date --ignore-space-change "${srcdir}"/0022-popen-call-usr-bin-sh-instead-of-bin-sh.patch
  git am --committer-date-is-author-date --ignore-space-change "${srcdir}"/0023-CI-add-a-GHA-for-doing-a-basic-build-test.patch
  git am --committer-date-is-author-date --ignore-space-change "${srcdir}"/0024-Set-up-a-GitHub-Action-to-keep-in-sync-with-Cygwin.patch
  git am --committer-date-is-author-date --ignore-space-change "${srcdir}"/0025-Expose-full-command-lines-to-other-Win32-processes-b.patch
  git am --committer-date-is-author-date --ignore-space-change "${srcdir}"/0026-Disable-the-cygwin-GitHub-workflow.patch
  git am --committer-date-is-author-date --ignore-space-change "${srcdir}"/0027-Do-not-show-Error-dialogs-by-default.patch
  git am --committer-date-is-author-date --ignore-space-change "${srcdir}"/0028-Add-a-helper-to-obtain-a-function-s-address-in-kerne.patch
  git am --committer-date-is-author-date --ignore-space-change "${srcdir}"/0029-Emulate-GenerateConsoleCtrlEvent-upon-Ctrl-C.patch
  git am --committer-date-is-author-date --ignore-space-change "${srcdir}"/0030-kill-kill-Win32-processes-more-gently.patch
  git am --committer-date-is-author-date --ignore-space-change "${srcdir}"/0031-Cygwin-make-option-for-native-inner-link-handling.patch
  git am --committer-date-is-author-date --ignore-space-change "${srcdir}"/0032-docs-skip-building-texinfo-and-PDF-files.patch
  git am --committer-date-is-author-date --ignore-space-change "${srcdir}"/0033-install-libs-depend-on-the-toollibs.patch
  git am --committer-date-is-author-date --ignore-space-change "${srcdir}"/0034-POSIX-ify-the-SHELL-variable.patch
  git am --committer-date-is-author-date --ignore-space-change "${srcdir}"/0035-Handle-ORIGINAL_PATH-just-like-PATH.patch
  git am --committer-date-is-author-date --ignore-space-change "${srcdir}"/0036-uname-allow-setting-the-system-name-to-CYGWIN.patch
  git am --committer-date-is-author-date --ignore-space-change "${srcdir}"/0037-Implicitly-support-the-dev-fd-symlink-and-friends.patch
  git am --committer-date-is-author-date --ignore-space-change "${srcdir}"/0038-Fix-incorrect-path-conversion-trailing-slash.patch
  git am --committer-date-is-author-date --ignore-space-change "${srcdir}"/0039-Pass-environment-variables-with-empty-values.patch
  git am --committer-date-is-author-date --ignore-space-change "${srcdir}"/0040-Handle-8-bit-characters-under-LOCALE-C.patch
  git am --committer-date-is-author-date --ignore-space-change "${srcdir}"/0041-Mention-the-extremely-useful-small_printf-function.patch
  git am --committer-date-is-author-date --ignore-space-change "${srcdir}"/0042-Add-a-helpful-debug-message-for-posix-to-windows-con.patch
  git am --committer-date-is-author-date --ignore-space-change "${srcdir}"/0043-Stop-assuming-that-there-are-no-spaces-in-POSIX-styl.patch
  git am --committer-date-is-author-date --ignore-space-change "${srcdir}"/0044-Avoid-unnecessary-recursion-in-find_path_start_and_t.patch
  git am --committer-date-is-author-date --ignore-space-change "${srcdir}"/0045-Leave-arguments-starting-with-a-tilde-or-quote-alone.patch
  git am --committer-date-is-author-date --ignore-space-change "${srcdir}"/0046-Leave-Git-s-name-and-message-arguments-alone-please.patch
  git am --committer-date-is-author-date --ignore-space-change "${srcdir}"/0047-Leave-paths-containing-any-special-characters-alone.patch
  git am --committer-date-is-author-date --ignore-space-change "${srcdir}"/0048-Leave-paths-containing-alone.patch
  git am --committer-date-is-author-date --ignore-space-change "${srcdir}"/0049-Skip-posix-to-windows-conversion-when-is-seen.patch
  git am --committer-date-is-author-date --ignore-space-change "${srcdir}"/0050-Also-leave-Git-s-rev-.-name-syntax-alone.patch
  git am --committer-date-is-author-date --ignore-space-change "${srcdir}"/0051-Arguments-starting-with-are-no-paths.patch
  git am --committer-date-is-author-date --ignore-space-change "${srcdir}"/0052-Prevent-scp-style-arguments-from-being-mangled.patch
  git am --committer-date-is-author-date --ignore-space-change "${srcdir}"/0053-Fixed-path-converting-with-non-ascii-char.patch
  git am --committer-date-is-author-date --ignore-space-change "${srcdir}"/0054-path-conversion-Introduce-ability-to-switch-off-conv.patch
  git am --committer-date-is-author-date --ignore-space-change "${srcdir}"/0055-Allow-overriding-the-home-directory-via-the-HOME-var.patch
  git am --committer-date-is-author-date --ignore-space-change "${srcdir}"/0056-Respect-db_home-setting-even-for-the-SYSTEM-account.patch
  git am --committer-date-is-author-date --ignore-space-change "${srcdir}"/0057-Respect-the-db_home-env-setting-under-more-circumsta.patch
  git am --committer-date-is-author-date --ignore-space-change "${srcdir}"/0058-Allow-native-symlinks-to-non-existing-targets-in-nat.patch
  git am --committer-date-is-author-date --ignore-space-change "${srcdir}"/0059-Make-paths-WCS-MBS-conversion-explicit.patch
  git am --committer-date-is-author-date --ignore-space-change "${srcdir}"/0060-Use-MB_CUR_MAX-6-by-default.patch
  git am --committer-date-is-author-date --ignore-space-change "${srcdir}"/0061-Change-the-default-base-address-for-x86_64.patch
  git am --committer-date-is-author-date --ignore-space-change "${srcdir}"/0062-Do-not-try-to-sync-with-Cygwin.patch
  git am --committer-date-is-author-date --ignore-space-change "${srcdir}"/0063-Avoid-sharing-cygheaps-across-Cygwin-versions.patch
  git am --committer-date-is-author-date --ignore-space-change "${srcdir}"/0064-dumper-avoid-linker-problem-when-libbfd-depends-on-l.patch
}

build() {
  [[ -d "${srcdir}"/build-${CHOST} ]] && rm -rf "${srcdir}"/build-${CHOST}
  mkdir -p "${srcdir}"/build-${CHOST} && cd "${srcdir}"/build-${CHOST}

  # Gives more verbose compile output when debugging.
  local -a extra_config
  if check_option "debug" "y"; then
    export CCWRAP_VERBOSE=1
    OPTIM="-O0"
    extra_config+=(--enable-debugging)
  else
    OPTIM="-O2"
  fi

  CFLAGS="$OPTIM -pipe -ggdb -Wno-error=deprecated -Wno-error=stringop-truncation -Wno-error=missing-attributes -Wno-error=maybe-uninitialized" #-Wno-error=class-memaccess
  CXXFLAGS="$OPTIM -pipe -ggdb -Wno-error=deprecated -Wno-error=stringop-truncation -Wno-error=missing-attributes -Wno-error=maybe-uninitialized" #-Wno-error=class-memaccess

  (cd "${srcdir}/msys2-runtime/winsup" && ./autogen.sh)

  "${srcdir}"/msys2-runtime/configure \
    --prefix=/usr \
    --build=${CHOST} \
    --sysconfdir=/etc \
    "${extra_config[@]}"
  LC_ALL=C make

  if test -n "$SIGNTOOL"
  then
    eval "$SIGNTOOL" \
      ${PWD}/${CHOST}/winsup/cygwin/msys0.dll \
      ${PWD}/${CHOST}/winsup/utils/*.exe ${PWD}/${CHOST}/winsup/utils/mingw/*.exe
  fi

  LC_ALL=C make -j1 DESTDIR="${srcdir}"/dest install

  #pushd ${CHOST}/winsup/cygwin > /dev/null
  #LANG=C make libmsys2_s.a
  #cp libmsys2_s.a "${srcdir}"/dest/usr/${CHOST}/lib/
  #popd > /dev/null

  rm -rf "${srcdir}"/dest/etc
}

package_msys2-runtime() {
  pkgdesc="Posix emulation engine for Windows"
  groups=('base')
  options=('!strip')
  conflicts=('catgets' 'libcatgets')
  replaces=('catgets' 'libcatgets')
  #install=msys2-runtime.install

  mkdir -p "${pkgdir}"/usr
  cp -rf "${srcdir}"/dest/usr/bin "${pkgdir}"/usr/
  rm -f "${pkgdir}"/usr/bin/msys-2.0.dbg
  rm -f "${pkgdir}"/usr/bin/cyglsa-config
  rm -f "${pkgdir}"/usr/bin/cyglsa.dll
  rm -f "${pkgdir}"/usr/bin/cyglsa64.dll
  rm -f "${pkgdir}"/usr/bin/cygserver-config
  cp -rf "${srcdir}"/dest/usr/libexec "${pkgdir}"/usr/
  cp -rf "${srcdir}"/dest/usr/share "${pkgdir}"/usr/
}

package_msys2-runtime-devel() {
  pkgdesc="MSYS2 headers and libraries"
  groups=('msys2-devel')
  depends=("msys2-runtime=${pkgver}")
  options=('staticlibs' '!strip')
  conflicts=('libcatgets-devel')
  replaces=('libcatgets-devel')

  mkdir -p "${pkgdir}"/usr/bin
  cp -f "${srcdir}"/dest/usr/bin/msys-2.0.dbg "${pkgdir}"/usr/bin/
  cp -rLf "${srcdir}"/dest/usr/${CHOST}/include "${pkgdir}"/usr/
  rm -f "${pkgdir}"/usr/include/iconv.h
  rm -f "${pkgdir}"/usr/include/unctrl.h
  # provided by libtirpc
  rm -fr "${pkgdir}"/usr/include/rpc/

  cp -rLf "${srcdir}"/dest/usr/${CHOST}/lib "${pkgdir}"/usr/
}

# return 0
# To hack on this:
# cd /c/repo-MSYS2/msys2-runtime/
# pushd src/build-i686-pc-msys
# LANG=C make && make -j1 DESTDIR=/c/repo-MSYS2/msys2-runtime/src/dest install
# popd
# makepkg -sRLf
# pacman -U msys2-runtime*.xz

# Quicker:
# open cmd.exe
# set "PATH=C:\\msys32\\usr\\bin;%PATH%"
# E:
# pushd E:\m2\repo-MSYS2\msys2-runtime\src\build-i686-pc-msys\i686-pc-msys\winsup\cygwin
# C:/msys32/usr/bin/bash -c "LANG=C && make"
# copy /y new-msys-2.0.dll C:\msys32\usr\bin\msys-2.0.dll
# popd
# C:
# C:/msys32/usr/bin/strace ls / > C:/strace.txt 2>&1
#
