# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Ray Donnelly <mingw.android@gmail.com>

pkgbase=msys2-runtime
pkgname=('msys2-runtime' 'msys2-runtime-devel')
pkgver=3.6.5
pkgrel=1
pkgdesc="Cygwin POSIX emulation engine"
arch=('x86_64')
url="https://www.cygwin.com/"
license=('GPL')
msys2_references=(
  'cygwin: cygwin'
  "cpe: cpe:/a:cygwin:cygwin"
)
makedepends=('cocom'
             'git'
             'perl'
             'gcc'
             'mingw-w64-cross-crt'
             'mingw-w64-cross-gcc'
             'mingw-w64-cross-zlib'
             'zlib-devel'
             'gettext-devel'
             'libiconv-devel'
             'autotools'
             'xmlto'
             'docbook-xsl')
source=('msys2-runtime'::git+https://github.com/cygwin/cygwin#tag=cygwin-${pkgver}
        msys2-runtime.commit
        0001-Mention-the-extremely-useful-small_printf-function.patch
        0002-Allow-native-symlinks-to-non-existing-targets-in-nat.patch
        0003-WIP-Handle-8-bit-characters-under-LOCALE-C.patch
        0004-Make-paths-WCS-MBS-conversion-explicit.patch
        0005-Use-MB_CUR_MAX-6-by-default.patch
        0006-msys2-runtime-restore-fast-path-for-current-user-pri.patch
        0007-Fix-msys-library-name-in-import-libraries.patch
        0008-Rename-dll-from-cygwin-to-msys.patch
        0009-Add-functionality-for-converting-UNIX-paths-in-argum.patch
        0010-Add-functionality-for-changing-OS-name-via-MSYSTEM-e.patch
        0011-Move-root-to-usr.-Change-sorting-mount-points.-By-de.patch
        0012-Instead-of-creating-Cygwin-symlinks-use-deep-copy-by.patch
        0013-Automatically-rewrite-TERM-msys-to-TERM-cygwin.patch
        0014-Do-not-convert-environment-for-strace.patch
        0015-strace.cc-Don-t-set-MSYS-noglob.patch
        0016-Add-debugging-for-strace-make_command_line.patch
        0017-strace-quiet-be-really-quiet.patch
        0018-path_conv-special-case-root-directory-to-have-traili.patch
        0019-When-converting-to-a-Unix-path-avoid-double-trailing.patch
        0020-msys2_path_conv-pass-PC_NOFULL-to-path_conv.patch
        0021-path-conversion-Introduce-ability-to-switch-off-conv.patch
        0022-dcrt0.cc-Untangle-allow_glob-from-winshell.patch
        0023-dcrt0.cc-globify-Don-t-quote-literal-strings-differe.patch
        0024-Add-debugging-for-build_argv.patch
        0025-environ.cc-New-facility-environment-variable-MSYS2_E.patch
        0026-Introduce-the-enable_pcon-value-for-MSYS.patch
        0027-popen-call-usr-bin-sh-instead-of-bin-sh.patch
        0028-Expose-full-command-lines-to-other-Win32-processes-b.patch
        0029-Add-a-helper-to-obtain-a-function-s-address-in-kerne.patch
        0030-Emulate-GenerateConsoleCtrlEvent-upon-Ctrl-C.patch
        0031-kill-kill-Win32-processes-more-gently.patch
        0032-Cygwin-make-option-for-native-inner-link-handling.patch
        0033-docs-skip-building-texinfo-and-PDF-files.patch
        0034-install-libs-depend-on-the-toollibs.patch
        0035-POSIX-ify-the-SHELL-variable.patch
        0036-Handle-ORIGINAL_PATH-just-like-PATH.patch
        0037-uname-allow-setting-the-system-name-to-CYGWIN.patch
        0038-Pass-environment-variables-with-empty-values.patch
        0039-Optionally-disallow-empty-environment-values-again.patch
        0040-build_env-respect-the-MSYS-environment-variable.patch
        0041-Revert-Cygwin-Enable-dynamicbase-on-the-Cygwin-DLL-b.patch
        0042-Avoid-sharing-cygheaps-across-Cygwin-versions.patch
        0043-uname-report-msys2-runtime-commit-hash-too.patch
        0044-Cygwin-Adjust-CWD-magic-to-accommodate-for-the-lates.patch
        0045-fixup-Add-functionality-for-converting-UNIX-paths-in.patch
        0046-Change-the-default-base-address-for-x86_64.patch)
sha256sums=('8c563e2cdbb1be14eadd44ff806e5f115575cf6502e4d9aed86070ae644441bc'
            '2a6e001efe3aad241d0f8c5d53e05ac00fb823f947406780bf0ba22a3646a49d'
            '79b6ee3b769886e49f28d2f8079493e2845f6c14354c6ba42ce9ed47d93147dc'
            'b06214c0e66ef3a3ac93485305eb201aa130ecd1b0161b679cd777175c9b05ba'
            '86783b1c51cb1c0f483a6e74b1f8a165dff73907939798bbbf8bf06346e491f5'
            '824050ec952d64e4c54566c8ce7cf2585c6704a6c3cbcb1c5500f3e4777fca0a'
            'b3bdb60c6ae7f2e64f2dd6eb54864a5ad840917f3be818d8165cebb0482fd787'
            '1f62e630e98133772624f60a855ef6c0dd8138e121d5e725e0b52070d4be0618'
            '36c1cac78144de8cc122bba40ddc11266f8657879fcd3230135ecb04799f63b0'
            '25b3bec54dac1ecc844f249ac759dade99c8f3005c412bcad2310fef8235be1e'
            '89e3963ddc270f5bc7ccc8985965c632608ebdce094b841fc32e2a54baa952bf'
            'a30bd23b2260bf6fec6ab09f7a19aaf7ce7304b974280b6dc9fc653a668d892b'
            '0fba87e5c0e82044f076cdd62d34d16cdf52bd126b38791d5c2313aacd7c6d29'
            '7164b5fd2adc20fbf83c133607431bedd93003e9242edf6f6dda700145e6077f'
            '9176f602f89011c70e0223bb0422c5715b91dbbe287a26206ffcb684d51ca221'
            '93c28f2820ed8c2c852a1e163567f963ebf0b8328e368f3315fad7bb0a767e53'
            '3c8baeb37aa0059af479ba1c98c76ee71dc00b70bab4e7fb47cc45361cc01f47'
            '9f21aec99b9a79c8b1a1cc7cf798a1263de61cddf7b6359a55666ab57a4bc38f'
            'd68cd0b7e0c98673abb5a78a6e98daccd2bf575f92b62751db0e462e66247d59'
            '98b694abafe0ff6e9ce9dcbc39a2a9461ff735d897b805650d0f3bdf7bb9bee5'
            '8e09a938b61b9dcd7b787138bc4e30861be28c584e9afeffdc6ecb6b27d4ff9f'
            'ae5d77339f3d8c0ff6d1004bce80d7d16c1448454aeed9f7ee978c5e54482de7'
            '16042a109d5bfe1a533c17ed72fb8d9f11ea5df2338f476e2ac7106fabc3ae07'
            '7cab0c1d8e3587fd012de29d4784d0ab2d77f44fe31aabb1165eb2568e743d18'
            'e543709d287198c3a3130a4e1a866bbbee3c1edc27d2734fe8134235c66a48d2'
            '53a6b96372fbf2fee14ddc3a9a6dbe380b279875330365ab7dde4964a1115e17'
            '2ae3fed25ff0547a2fb31c4e54381b8e54778660efb43103d171eaf49cdb6815'
            '784638bc9fbac49868d1a483dee1591dfd16fef3da1f5eda540e49b5c4d36d47'
            '932e8e25897c3bc567c800a5be1c296555b07dddf9ada052711dbe21030b1115'
            'c102f429d2dba9c14e9d60ba60f7e26ef03c08ffcb4d48f232fb68623d3223eb'
            '8a9b23e12844f069f0f40e8a436300ca9d8c69f9571d5ae4b997b6c0ef7ff61c'
            '957ac2e8b3fbac31bb742feaa3e48ad5a09051c046cae2f5a50af8b7c7ef83b0'
            '82e6b1aab710622fe7aa034769e8150d34b5b0e1bd72705de1ba8bebfdaa9558'
            'ed15a0910c5d994dc0d4b08407a44f0a97f39392c328b5173fcdd9d0e25a013d'
            'b52b768cb236363286bf8205d0d915162167de4e11a94111ec60d36d3e5d3156'
            'ad254dbc2732095247f12eb4f7a00bb6bcac099268a610be7b0bbe782010ee6e'
            '98cc8a2dfd8fbd2b6a2390a16c177583d4d1c77a6fba4d165f65873c8602107e'
            '720c047842771b8c1d269c403bbf2d6c12af3bc858e8b4a47dd40c2d1efdcd1c'
            '9ce2f27543da45c2dd27950f06952dbdadc3ff5b763334b2c1a972ce2e3a7b78'
            '56070bf26014f9e90101a653c52b31a2d1ed3194dbee69b9a0b930df90eefd22'
            '778c696f235d77b4886dcf66ab51120bdc7cb78f7253e3ebaff7853080ec02f5'
            '9ae9544bdd9c5b7bbecdd83606bda937795a6474c7daba0b2c032f0122ebff9a'
            '16d7506a767ffd5f99b4f8be5bb747e7159769e3b58d2896752b3d466411c7a7'
            '08b4d3ddf65bf2c823d4f45ee3d79d34ee1f6fa50fa68059523f19c6e0ce64d5'
            '413175606d64a9584309021ed5a70683de24dd4e086280d89c4415f81a42bad7'
            'b9b14753b2302e13253f51ca977cb44ce781c1e1f8aae86b3d2357d9133acec4'
            '41984c78b1fb7db10cf806f188cb38e2d99d30dbb871931d7c7ea477d7022df8'
            '2154e4dac82db3456fa36c6b0c7d6c1cbc2ef1e69c10e142c4f66e5f7100d9b8')

# Helper macros to help make tasks easier #
apply_patch_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying $_patch"
    patch -Nbp1 -i "${srcdir}/${_patch}"
  done
}

apply_git_am_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying $_patch"
    git apply "${srcdir}/${_patch}"
  done
}

del_file_exists() {
  for _fname in "$@"
  do
    if [ -f $_fname ]; then
      rm -rf $_fname
    fi
  done
}
# =========================================== #

prepare() {
  cd "${srcdir}"/msys2-runtime
  alt=.git/objects/info/alternates
  if grep '^/' $alt
  then
    echo "Fixing absolute paths"
    xargs -n 1 -r -a $alt -d '\n' realpath --relative-to=.git/objects >$alt.new
    mv -f $alt.new $alt
  fi
  if test true != "$(git config core.symlinks)"
  then
    git config core.symlinks true &&
    /usr/bin/git reset --hard
  fi
  if test false != "$(git config core.autocrlf)"
  then
    git config core.autocrlf false &&
    rm "$(git rev-parse --git-path index)" &&
    /usr/bin/git reset --hard
  fi
  del_file_exists winsup/cygwin/msys2_path_conv.cc \
    winsup/cygwin/msys2_path_conv.h \
    winsup/cygwin/include/cygwin/exit_process.h \
    winsup/utils/mingw/getprocaddr.c

  apply_git_am_with_msg \
 \
  0001-Mention-the-extremely-useful-small_printf-function.patch \
  0002-Allow-native-symlinks-to-non-existing-targets-in-nat.patch \
  0003-WIP-Handle-8-bit-characters-under-LOCALE-C.patch \
  0004-Make-paths-WCS-MBS-conversion-explicit.patch \
  0005-Use-MB_CUR_MAX-6-by-default.patch \
  0006-msys2-runtime-restore-fast-path-for-current-user-pri.patch \
  0007-Fix-msys-library-name-in-import-libraries.patch \
  0008-Rename-dll-from-cygwin-to-msys.patch \
  0009-Add-functionality-for-converting-UNIX-paths-in-argum.patch \
  0010-Add-functionality-for-changing-OS-name-via-MSYSTEM-e.patch \
  0011-Move-root-to-usr.-Change-sorting-mount-points.-By-de.patch \
  0012-Instead-of-creating-Cygwin-symlinks-use-deep-copy-by.patch \
  0013-Automatically-rewrite-TERM-msys-to-TERM-cygwin.patch \
  0014-Do-not-convert-environment-for-strace.patch \
  0015-strace.cc-Don-t-set-MSYS-noglob.patch \
  0016-Add-debugging-for-strace-make_command_line.patch \
  0017-strace-quiet-be-really-quiet.patch \
  0018-path_conv-special-case-root-directory-to-have-traili.patch \
  0019-When-converting-to-a-Unix-path-avoid-double-trailing.patch \
  0020-msys2_path_conv-pass-PC_NOFULL-to-path_conv.patch \
  0021-path-conversion-Introduce-ability-to-switch-off-conv.patch \
  0022-dcrt0.cc-Untangle-allow_glob-from-winshell.patch \
  0023-dcrt0.cc-globify-Don-t-quote-literal-strings-differe.patch \
  0024-Add-debugging-for-build_argv.patch \
  0025-environ.cc-New-facility-environment-variable-MSYS2_E.patch \
  0026-Introduce-the-enable_pcon-value-for-MSYS.patch \
  0027-popen-call-usr-bin-sh-instead-of-bin-sh.patch \
  0028-Expose-full-command-lines-to-other-Win32-processes-b.patch \
  0029-Add-a-helper-to-obtain-a-function-s-address-in-kerne.patch \
  0030-Emulate-GenerateConsoleCtrlEvent-upon-Ctrl-C.patch \
  0031-kill-kill-Win32-processes-more-gently.patch \
  0032-Cygwin-make-option-for-native-inner-link-handling.patch \
  0033-docs-skip-building-texinfo-and-PDF-files.patch \
  0034-install-libs-depend-on-the-toollibs.patch \
  0035-POSIX-ify-the-SHELL-variable.patch \
  0036-Handle-ORIGINAL_PATH-just-like-PATH.patch \
  0037-uname-allow-setting-the-system-name-to-CYGWIN.patch \
  0038-Pass-environment-variables-with-empty-values.patch \
  0039-Optionally-disallow-empty-environment-values-again.patch \
  0040-build_env-respect-the-MSYS-environment-variable.patch \
  0041-Revert-Cygwin-Enable-dynamicbase-on-the-Cygwin-DLL-b.patch \
  0042-Avoid-sharing-cygheaps-across-Cygwin-versions.patch \
  0043-uname-report-msys2-runtime-commit-hash-too.patch \
  0044-Cygwin-Adjust-CWD-magic-to-accommodate-for-the-lates.patch \
  0045-fixup-Add-functionality-for-converting-UNIX-paths-in.patch \
  0046-Change-the-default-base-address-for-x86_64.patch
}

build() {
  [[ -d "${srcdir}"/build-${CHOST} ]] && rm -rf "${srcdir}"/build-${CHOST}
  mkdir -p "${srcdir}"/build-${CHOST} && cd "${srcdir}"/build-${CHOST}

  # Gives more verbose compile output when debugging.
  local -a extra_config
  if check_option "debug" "y"; then
    export CCWRAP_VERBOSE=1
    OPTIM="-O0"
    extra_config+=(--enable-debugging)
  else
    OPTIM="-O2"
  fi

  # We use `-g0` here to suppress debug symbols, to avoid having to call `strip.exe`
  # on the binary files (which would break code-signing those files).
  CFLAGS="$OPTIM -pipe -g0"
  CXXFLAGS="$OPTIM -pipe -g0"

  # otherwise it asks git which appends "-dirty" because of our uncommited patches
  CFLAGS+=" -DCYGPORT_RELEASE_INFO=${pkgver}"

  (cd "${srcdir}/msys2-runtime/winsup" && ./autogen.sh)

  "${srcdir}"/msys2-runtime/configure \
    --with-msys2-runtime-commit="$(cat "${srcdir}/msys2-runtime.commit")" \
    --prefix=/usr \
    --build=${CHOST} \
    --sysconfdir=/etc \
    "${extra_config[@]}"
  LC_ALL=C make
  LC_ALL=C make -j1 DESTDIR="${srcdir}"/dest install

  rm -rf "${srcdir}"/dest/etc

  # split debug info from msys-2.0.dll
  cd "${srcdir}"/dest
  objcopy --add-gnu-debuglink=/dev/null --only-keep-debug usr/bin/msys-2.0.dll usr/bin/msys-2.0.dbg
  objcopy --strip-debug --strip-unneeded --add-gnu-debuglink=usr/bin/msys-2.0.dbg usr/bin/msys-2.0.dll usr/bin/msys-2.0.dll.new
  mv -f usr/bin/msys-2.0.dll.new usr/bin/msys-2.0.dll
}

package_msys2-runtime() {
  pkgdesc="Posix emulation engine for Windows"
  options=('!strip')
  conflicts=('catgets' 'libcatgets' 'msys2-runtime-3.6')
  replaces=('catgets' 'libcatgets' 'msys2-runtime-3.6')

  mkdir -p "${pkgdir}"/usr
  cp -rf "${srcdir}"/dest/usr/bin "${pkgdir}"/usr/
  cp -rf "${srcdir}"/dest/usr/libexec "${pkgdir}"/usr/
  rm -f "${pkgdir}"/usr/bin/msys-2.0.dbg
  rm -f "${pkgdir}"/usr/bin/cyglsa-config
  rm -f "${pkgdir}"/usr/bin/cyglsa.dll
  rm -f "${pkgdir}"/usr/bin/cyglsa64.dll
  rm -f "${pkgdir}"/usr/bin/cygserver-config
  cp -rf "${srcdir}"/dest/usr/share "${pkgdir}"/usr/
}

package_msys2-runtime-devel() {
  pkgdesc="MSYS2 headers and libraries"
  depends=("msys2-runtime=${pkgver}")
  conflicts=('libcatgets-devel' 'msys2-runtime-3.6-devel')
  replaces=('libcatgets-devel' 'msys2-runtime-3.6-devel')
  # strip breaks the split debug info.  msys2/msys2-pacman#52
  options=('!strip')

  mkdir -p "${pkgdir}"/usr/bin
  cp -f "${srcdir}"/dest/usr/bin/msys-2.0.dbg "${pkgdir}"/usr/bin/
  cp -rLf "${srcdir}"/dest/usr/${CHOST}/include "${pkgdir}"/usr/
  rm -f "${pkgdir}"/usr/include/iconv.h
  rm -f "${pkgdir}"/usr/include/unctrl.h
  # provided by libtirpc
  rm -fr "${pkgdir}"/usr/include/rpc/

  cp -rLf "${srcdir}"/dest/usr/${CHOST}/lib "${pkgdir}"/usr/

  # compatibility with Cygwin toolchains
  cp "${srcdir}"/dest/usr/${CHOST}/lib/libmsys-2.0.a "${pkgdir}"/usr/lib/libcygwin.a
}
