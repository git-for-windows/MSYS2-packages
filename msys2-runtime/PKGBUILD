# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Ray Donnelly <mingw.android@gmail.com>

pkgbase=msys2-runtime
pkgname=('msys2-runtime' 'msys2-runtime-devel')
pkgver=3.4.10
pkgrel=3
pkgdesc="Cygwin POSIX emulation engine"
arch=('x86_64')
url="https://www.cygwin.com/"
license=('GPL')
groups=('base')
makedepends=('cocom'
             'texinfo'
             'git'
             'perl'
             'gcc'
             'make'
             'mingw-w64-cross-crt-git'
             'mingw-w64-cross-gcc'
             'mingw-w64-cross-zlib'
             'zlib-devel'
             'gettext-devel'
             'libiconv-devel'
             'autotools'
             'xmlto'
             'docbook-xsl')
source=('msys2-runtime'::git+https://github.com/cygwin/cygwin#tag=cygwin-${pkgver}
        msys2-runtime.commit
        0001-Add-MSYS2-triplet.patch
        0002-Fix-msys-library-name-in-import-libraries.patch
        0003-Rename-dll-from-cygwin-to-msys.patch
        0004-Add-functionality-for-converting-UNIX-paths-in-argum.patch
        0005-Add-functionality-for-changing-OS-name-via-MSYSTEM-e.patch
        0006-Move-root-to-usr.-Change-sorting-mount-points.-By-de.patch
        0007-Instead-of-creating-Cygwin-symlinks-use-deep-copy-by.patch
        0008-Automatically-rewrite-TERM-msys-to-TERM-cygwin.patch
        0009-Do-not-convert-environment-for-strace.patch
        0010-path_conv-special-case-root-directory-to-have-traili.patch
        0011-dcrt0.cc-Untangle-allow_glob-from-winshell.patch
        0012-dcrt0.cc-globify-Don-t-quote-literal-strings-differe.patch
        0013-strace.cc-Don-t-set-MSYS-noglob.patch
        0014-Add-debugging-for-build_argv.patch
        0015-Add-debugging-for-strace-make_command_line.patch
        0016-environ.cc-New-facility-environment-variable-MSYS2_E.patch
        0017-Fix-native-symbolic-link-spawn-passing-wrong-arg0.patch
        0018-strace-quiet-be-really-quiet.patch
        0019-Introduce-the-enable_pcon-value-for-MSYS.patch
        0020-popen-call-usr-bin-sh-instead-of-bin-sh.patch
        0021-Expose-full-command-lines-to-other-Win32-processes-b.patch
        0022-Do-not-show-Error-dialogs-by-default.patch
        0023-Add-a-helper-to-obtain-a-function-s-address-in-kerne.patch
        0024-Emulate-GenerateConsoleCtrlEvent-upon-Ctrl-C.patch
        0025-kill-kill-Win32-processes-more-gently.patch
        0026-Cygwin-make-option-for-native-inner-link-handling.patch
        0027-docs-skip-building-texinfo-and-PDF-files.patch
        0028-install-libs-depend-on-the-toollibs.patch
        0029-POSIX-ify-the-SHELL-variable.patch
        0030-Handle-ORIGINAL_PATH-just-like-PATH.patch
        0031-uname-allow-setting-the-system-name-to-CYGWIN.patch
        0032-Pass-environment-variables-with-empty-values.patch
        0033-Optionally-disallow-empty-environment-values-again.patch
        0034-build_env-respect-the-MSYS-environment-variable.patch
        0035-When-converting-to-a-Unix-path-avoid-double-trailing.patch
        0036-msys2_path_conv-pass-PC_NOFULL-to-path_conv.patch
        0037-Revert-Cygwin-Enable-dynamicbase-on-the-Cygwin-DLL-b.patch
        0038-dumper-avoid-linker-problem-when-libbfd-depends-on-l.patch
        0039-Handle-8-bit-characters-under-LOCALE-C.patch
        0040-Mention-the-extremely-useful-small_printf-function.patch
        0041-Leave-arguments-starting-with-a-tilde-or-quote-alone.patch
        0042-Leave-Git-s-name-and-message-arguments-alone-please.patch
        0043-Leave-paths-containing-any-special-characters-alone.patch
        0044-Leave-paths-containing-alone.patch
        0045-Skip-posix-to-windows-conversion-when-is-seen.patch
        0046-Also-leave-Git-s-rev-.-name-syntax-alone.patch
        0047-Arguments-starting-with-are-no-paths.patch
        0048-Prevent-scp-style-arguments-from-being-mangled.patch
        0049-Fixed-path-converting-with-non-ascii-char.patch
        0050-path-conversion-Introduce-ability-to-switch-off-conv.patch
        0051-Allow-overriding-the-home-directory-via-the-HOME-var.patch
        0052-Respect-db_home-setting-even-for-the-SYSTEM-account.patch
        0053-Respect-the-db_home-env-setting-under-more-circumsta.patch
        0054-Allow-native-symlinks-to-non-existing-targets-in-nat.patch
        0055-Make-paths-WCS-MBS-conversion-explicit.patch
        0056-Use-MB_CUR_MAX-6-by-default.patch
        0057-Change-the-default-base-address-for-x86_64.patch
        0058-Avoid-sharing-cygheaps-across-Cygwin-versions.patch
        0059-uname-report-msys2-runtime-commit-hash-too.patch
        0060-Revert-Cygwin-pipe-Restore-blocking-mode-for-cygwin-.patch
        0061-msys2-runtime-restore-fast-path-for-current-user-pri.patch
        0062-Work-around-fragile-include-in-binutils.patch
        0063-Cygwin-console-Avoid-slipping-past-disable_master_th.patch
        0064-Cygwin-pty-Fix-handle-leak-in-master-process.patch
        0065-Cygwin-pty-Fix-potential-handle-leak-regarding-CallN.patch
        0066-Cygwin-console-Make-VMIN-and-VTIME-work.patch
        0067-Cygwin-suppress-a-warning-generated-with-w32api-12.0.patch)
sha256sums=('4fb94e9f3bab9666dcab50c8940de6e8801d0aae900f0b5a6f79406479e757f1'
            '06e14933861636ce850cf4cfb1a28abca7393e3efd6271981265a643e00b5d28'
            '351bb1efdbdafe80c981e92d6b425c6ab71c85ce4e990db184e2118158eb2ab6'
            'd3d3a01feeae9f7d5e6cb32f4662df74fc9476ff11a1aac3dad2df3e43fd88e4'
            '2e50ecd65f2fd413baaf39e5058a6b252245abc7d34f4ebf17dd4f7ffed60ced'
            '231757d797cf9be6a92b6f6976ee85216e9fa8eaba4aca241ace4b76e41cd862'
            '23f97fac32ae2ba7681985e0d4910ec2d6d265f970bb0db6cec48540b6ebf055'
            'd47c570e2ac7a9b1c15f1a9e4e51b530eef0a2dc0f261ddc088ad449840deb02'
            '1bc7e3275eb9ffb3b70a0b9a5049bc6180d893d28ac6fc1d0e2ca29f8d890a33'
            '8113de5e8ae37da6b96a3c7eb379806ccb1a90f42ae0d9061941d5e224f754a0'
            '09ec71a8292035b38e5fb6db7fdd4f2fb6c5071ec9a7c180c72bb22216db88df'
            '02cc70007deccabac988ae02a82b201a71772b0dc6570b0275793f37f71ef09d'
            'b70c4cc2f37632d51e9ddcae56f12c9ec92357e11347cb806ed23af9251a601a'
            '51571b603ca6a75f22125daeb1973a9f8ecdf82b1d4637814287fa311f9480c3'
            'd4dbcdbfa4f2dbac9a06eac0cd42154e30df03466554778340dadf67ab861c8b'
            '5c018ce376a7f2b0b8e3dc3a4eedc5eebf618ef2337889188e6f914d0ac09795'
            '2350b6193166cab2596162d8a203a4edbddb413a615e84f7ecc120ffa98f609f'
            '7d25457a43facd4231aad09bc5acf4717468a937c087427bd8cc5e2a1046db84'
            '233b59d7ca0a339e09c15f7c64989ba347f1316f353d2d366dede33325b4091f'
            'b4eeb001292c5a34df2ebe65d8deef987eb70baf60f821d86c3cbccb49fea40a'
            '3afbea60cc0edfc7931ecaafeb1d6b7dfda1aac10be590afd41abff8a27815ef'
            '691328ad116d972eeb0d04558c68ad605e306faec26a3e43de90024e31d797d6'
            'b36301669c8e2802cc82c8ac7b99b10135f0aee12e3b01b258272e7089a9c4ed'
            '231d6372dcbdb72f4cd10dad60bbdf9f4d55236193e5cbfc2377dd1af1194360'
            'c6cfb73dab426e9b7649514408d4a0577ebac43aef9c0585e720e5624c2a904e'
            'aa3c148a0655d47d9acd822547ef4e8ef35be309e71e7d85c4af8bb3a485f7db'
            '4318b3e6e99f724c211b0c8b6ff93d644336a08fd29219381de9035b8f7b5415'
            '4cf58088663b156ea6ca2278eb24226ef3f57f9bd486dea9b1ac7b04fa62eb0c'
            '44d207e6e7cda10d427b6705735622fd7847a58d4f133862e9f82f11fd57cf0c'
            '161e4bcbf55f5d4b81bdbed73e0c8fdc339c75b70cd9e292fde41b53988bfb5b'
            '5528c8e0715404fc03068efcd8c42010604fe5940a7af962211066f9670276b5'
            'd121cbfed47c72787c92eb817bba2071930f86fdf2bff424d1082ceccd59da49'
            '832aaa93defa111e3d9f540532288398ad27352122b1ff84a8a2f53232e601b9'
            'de44e80e60e61dbf91b6b99a3f5740f653f83b96c64f862f4aa99fd42e7db0d7'
            '7cebdd623fe080bda89b0505d4e8dc8600969d3b7d71e6bdf0a7e6ac1c62708f'
            '008adf33a2b3774d85aabe1b2b2a3fa32615c0cf7bfbc7cf4e4ce3c0302957d6'
            'd5981d518ba4756dd6e976e9cc20ff8f52001aac588ee083a33045d260e02d6b'
            '599905c7454e2f3039c717de8186d4e1cb7943f9b47a7745ce36e4acfa100e31'
            'd37c835bc188786a4e8050f0a73d4f0b3070ded9030943ba2a6c47b03d9d96e8'
            'a799e7edde58a0ec519bb7df6d0bcf4177e1c6236a72a8240936c574be1bdd61'
            '31ff5a6708220857530434c06b454b92e9854959ba1be3851ec935ec13b0658d'
            '4f2a4d56e94e73b7c2e390b1094290c475f7661884f4a8ad2bbae2be9a450b04'
            '0498c9fd1cca930b2864783297de73528c646aad8a3e2efa2749950d079d9b25'
            'a0abcdf33b992cc1c3cd1258cd255bdbe07095e7b41f497633630d1de837444e'
            'b092095b050ee2a34224d973ef4086309d2df57ff979155f1837b27df24202d8'
            '28e0d46690483ac9259f56db795411047afd40cc1ef249945d436c512436cc6a'
            '1d0ab13576f3985f78632a1fa143287673847ea8ad24b41f57972ebecfa69f41'
            'dd29a84985a2ca96681d742b4c510f4945be4998c8af5fe3df0a1d0dea8c28e6'
            'c95e31fbc3b2466e422b872eb76d2e849e0ad2d9ce750e5d0afb31a5ae2f01b4'
            '6c5fcffa44adbffc08e80a1a941c73b5034612ce70d2485face45672b6875186'
            'f1815f8a275f5e00275a8fd89a96ac71f5f9a5d7bd6dd408f33a42d2e0edb32b'
            'f3c551c5395ba8d77ab19583f538e5802d022f37c29d2a8566737546ca426888'
            'fccabdff9e3a1bac75a45a0b4cff3a0a956988ea4cea5d3674daa9023d33025c'
            'cc1b7fe86a247980204b054885d27a65cac8ef10c542357a322edfb9ae6eb567'
            '8798d6230a35863a28f40cb37ed4cd4dfd02cdadaf383829b6d4dbbcdc464912'
            '5bd11782eec27ccb9fb2452d5d2d31ede5db163cade0c3f54729cd426a57e4b5'
            '73af0906b90ba60e8ab6c12f0b3447944e4512a9dcaae23899d39cf4a5949070'
            'c75d7977db74781e7a3e756c555a0fac8b3a27949c815456bec00f6c7ec0e6f6'
            '99ba3654fbe2e60019fe9ba57f84d7ad5264c673ba585b8bafb4588cde667b80'
            '119511d1c5412616e2fc4142384025e74c71817f421e316895bfbb1b0b16adec'
            'c261206c44323bb14329d9d9b04b3baa0d3544d6b119ec95df6872bebb4f8dc6'
            'fef601c95cb0893ddc9b3479721d298c08e4e1cc1840c89330328e2be09e5ccd'
            '4916e58433cb4df408c2068e11ff066925af8558341a38f99a216ab8b2871552'
            '322e300dd638a18cce5c373ca85fc374e21c0cb191faf847bd7f91ee730892ad'
            'd309a8ea69a0b1b173d56e5bb5a0003613a937d48c6d7736a5ed358ac25c1ef4'
            'f661eb5b8ea9270888ad8bb129b307af5408fbf02a046bed91692666b54fc610'
            '8177e7300bcd0a2d96de2d29faf0b21a7fec74c77ac0ac66bbb8244e37efb541'
            '22a99a0f9f1b90ab6d7fb1cee76c76f7c82d2185a5d0acf7bd60553f54fc9e80'
            '11c89890557f23dfaf754bc11381d8fd82e3db0755d56df6b6e30e018d2c3ef3')

# Helper macros to help make tasks easier #
apply_patch_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying $_patch"
    patch -Nbp1 -i "${srcdir}/${_patch}"
  done
}

apply_git_am_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying $_patch"
    git apply "${srcdir}/${_patch}"
  done
}

del_file_exists() {
  for _fname in "$@"
  do
    if [ -f $_fname ]; then
      rm -rf $_fname
    fi
  done
}
# =========================================== #

prepare() {
  cd "${srcdir}"/msys2-runtime
  alt=.git/objects/info/alternates
  if grep '^/' $alt
  then
    echo "Fixing absolute paths"
    xargs -n 1 -r -a $alt -d '\n' realpath --relative-to=.git/objects >$alt.new
    mv -f $alt.new $alt
  fi
  if test true != "$(git config core.symlinks)"
  then
    git config core.symlinks true &&
    /usr/bin/git reset --hard
  fi
  if test false != "$(git config core.autocrlf)"
  then
    git config core.autocrlf false &&
    rm "$(git rev-parse --git-path index)" &&
    /usr/bin/git reset --hard
  fi
  del_file_exists winsup/cygwin/msys2_path_conv.cc \
    winsup/cygwin/msys2_path_conv.h \
    winsup/cygwin/include/cygwin/exit_process.h \
    winsup/utils/mingw/getprocaddr.c
  apply_git_am_with_msg \
 \
  0001-Add-MSYS2-triplet.patch \
  0002-Fix-msys-library-name-in-import-libraries.patch \
  0003-Rename-dll-from-cygwin-to-msys.patch \
  0004-Add-functionality-for-converting-UNIX-paths-in-argum.patch \
  0005-Add-functionality-for-changing-OS-name-via-MSYSTEM-e.patch \
  0006-Move-root-to-usr.-Change-sorting-mount-points.-By-de.patch \
  0007-Instead-of-creating-Cygwin-symlinks-use-deep-copy-by.patch \
  0008-Automatically-rewrite-TERM-msys-to-TERM-cygwin.patch \
  0009-Do-not-convert-environment-for-strace.patch \
  0010-path_conv-special-case-root-directory-to-have-traili.patch \
  0011-dcrt0.cc-Untangle-allow_glob-from-winshell.patch \
  0012-dcrt0.cc-globify-Don-t-quote-literal-strings-differe.patch \
  0013-strace.cc-Don-t-set-MSYS-noglob.patch \
  0014-Add-debugging-for-build_argv.patch \
  0015-Add-debugging-for-strace-make_command_line.patch \
  0016-environ.cc-New-facility-environment-variable-MSYS2_E.patch \
  0017-Fix-native-symbolic-link-spawn-passing-wrong-arg0.patch \
  0018-strace-quiet-be-really-quiet.patch \
  0019-Introduce-the-enable_pcon-value-for-MSYS.patch \
  0020-popen-call-usr-bin-sh-instead-of-bin-sh.patch \
  0021-Expose-full-command-lines-to-other-Win32-processes-b.patch \
  0022-Do-not-show-Error-dialogs-by-default.patch \
  0023-Add-a-helper-to-obtain-a-function-s-address-in-kerne.patch \
  0024-Emulate-GenerateConsoleCtrlEvent-upon-Ctrl-C.patch \
  0025-kill-kill-Win32-processes-more-gently.patch \
  0026-Cygwin-make-option-for-native-inner-link-handling.patch \
  0027-docs-skip-building-texinfo-and-PDF-files.patch \
  0028-install-libs-depend-on-the-toollibs.patch \
  0029-POSIX-ify-the-SHELL-variable.patch \
  0030-Handle-ORIGINAL_PATH-just-like-PATH.patch \
  0031-uname-allow-setting-the-system-name-to-CYGWIN.patch \
  0032-Pass-environment-variables-with-empty-values.patch \
  0033-Optionally-disallow-empty-environment-values-again.patch \
  0034-build_env-respect-the-MSYS-environment-variable.patch \
  0035-When-converting-to-a-Unix-path-avoid-double-trailing.patch \
  0036-msys2_path_conv-pass-PC_NOFULL-to-path_conv.patch \
  0037-Revert-Cygwin-Enable-dynamicbase-on-the-Cygwin-DLL-b.patch \
  0038-dumper-avoid-linker-problem-when-libbfd-depends-on-l.patch \
  0039-Handle-8-bit-characters-under-LOCALE-C.patch \
  0040-Mention-the-extremely-useful-small_printf-function.patch \
  0041-Leave-arguments-starting-with-a-tilde-or-quote-alone.patch \
  0042-Leave-Git-s-name-and-message-arguments-alone-please.patch \
  0043-Leave-paths-containing-any-special-characters-alone.patch \
  0044-Leave-paths-containing-alone.patch \
  0045-Skip-posix-to-windows-conversion-when-is-seen.patch \
  0046-Also-leave-Git-s-rev-.-name-syntax-alone.patch \
  0047-Arguments-starting-with-are-no-paths.patch \
  0048-Prevent-scp-style-arguments-from-being-mangled.patch \
  0049-Fixed-path-converting-with-non-ascii-char.patch \
  0050-path-conversion-Introduce-ability-to-switch-off-conv.patch \
  0051-Allow-overriding-the-home-directory-via-the-HOME-var.patch \
  0052-Respect-db_home-setting-even-for-the-SYSTEM-account.patch \
  0053-Respect-the-db_home-env-setting-under-more-circumsta.patch \
  0054-Allow-native-symlinks-to-non-existing-targets-in-nat.patch \
  0055-Make-paths-WCS-MBS-conversion-explicit.patch \
  0056-Use-MB_CUR_MAX-6-by-default.patch \
  0057-Change-the-default-base-address-for-x86_64.patch \
  0058-Avoid-sharing-cygheaps-across-Cygwin-versions.patch \
  0059-uname-report-msys2-runtime-commit-hash-too.patch \
  0060-Revert-Cygwin-pipe-Restore-blocking-mode-for-cygwin-.patch \
  0061-msys2-runtime-restore-fast-path-for-current-user-pri.patch \
  0062-Work-around-fragile-include-in-binutils.patch \
  0063-Cygwin-console-Avoid-slipping-past-disable_master_th.patch \
  0064-Cygwin-pty-Fix-handle-leak-in-master-process.patch \
  0065-Cygwin-pty-Fix-potential-handle-leak-regarding-CallN.patch \
  0066-Cygwin-console-Make-VMIN-and-VTIME-work.patch \
  0067-Cygwin-suppress-a-warning-generated-with-w32api-12.0.patch
}

build() {
  [[ -d "${srcdir}"/build-${CHOST} ]] && rm -rf "${srcdir}"/build-${CHOST}
  mkdir -p "${srcdir}"/build-${CHOST} && cd "${srcdir}"/build-${CHOST}

  # Gives more verbose compile output when debugging.
  local -a extra_config
  if check_option "debug" "y"; then
    export CCWRAP_VERBOSE=1
    OPTIM="-O0"
    extra_config+=(--enable-debugging)
  else
    OPTIM="-O2"
  fi

  CFLAGS="$OPTIM -pipe -g0 -Wno-error=deprecated -Wno-error=stringop-truncation -Wno-error=missing-attributes -Wno-error=maybe-uninitialized" #-Wno-error=class-memaccess
  CXXFLAGS="$OPTIM -pipe -g0 -Wno-error=deprecated -Wno-error=stringop-truncation -Wno-error=missing-attributes -Wno-error=maybe-uninitialized" #-Wno-error=class-memaccess

  # otherwise it asks git which appends "-dirty" because of our uncommited patches
  CFLAGS+=" -DCYGPORT_RELEASE_INFO=${pkgver}"

  # XXX: cygwin still uses gcc v11 so we get new warnings with v13,
  # resulting in errors. We can't selectively disable warnigns since our
  # cross compiler is also too old and doesn't understand the new
  # warning flags, so we need to disable all errors for now.
  export CXXFLAGS="-Wno-error -Wno-narrowing"

  (cd "${srcdir}/msys2-runtime/winsup" && ./autogen.sh)

  "${srcdir}"/msys2-runtime/configure \
    --with-msys2-runtime-commit="$(cat "${srcdir}/msys2-runtime.commit")" \
    --prefix=/usr \
    --build=${CHOST} \
    --sysconfdir=/etc \
    "${extra_config[@]}"
  LC_ALL=C make

  if test -n "$SIGNTOOL"
  then
    eval "$SIGNTOOL" \
      ${PWD}/${CHOST}/winsup/cygwin/new-msys-2.0.dll \
      ${PWD}/${CHOST}/winsup/utils/*.exe ${PWD}/${CHOST}/winsup/utils/mingw/*.exe
  fi

  LC_ALL=C make -j1 DESTDIR="${srcdir}"/dest install

  #pushd ${CHOST}/winsup/cygwin > /dev/null
  #LANG=C make libmsys2_s.a
  #cp libmsys2_s.a "${srcdir}"/dest/usr/${CHOST}/lib/
  #popd > /dev/null

  rm -rf "${srcdir}"/dest/etc
}

package_msys2-runtime() {
  pkgdesc="Posix emulation engine for Windows"
  groups=('base')
  options=('!strip')
  conflicts=('catgets' 'libcatgets' 'msys2-runtime-3.4')
  replaces=('catgets' 'libcatgets' 'msys2-runtime-3.4')

  mkdir -p "${pkgdir}"/usr
  cp -rf "${srcdir}"/dest/usr/bin "${pkgdir}"/usr/
  cp -rf "${srcdir}"/dest/usr/libexec "${pkgdir}"/usr/
  rm -f "${pkgdir}"/usr/bin/cyglsa-config
  rm -f "${pkgdir}"/usr/bin/cyglsa.dll
  rm -f "${pkgdir}"/usr/bin/cyglsa64.dll
  rm -f "${pkgdir}"/usr/bin/cygserver-config
  cp -rf "${srcdir}"/dest/usr/share "${pkgdir}"/usr/
}

package_msys2-runtime-devel() {
  pkgdesc="MSYS2 headers and libraries"
  groups=('msys2-devel')
  depends=("msys2-runtime=${pkgver}")
  conflicts=('libcatgets-devel' 'msys2-runtime-3.4-devel')
  replaces=('libcatgets-devel' 'msys2-runtime-3.4-devel')

  mkdir -p "${pkgdir}"/usr/bin
  cp -rLf "${srcdir}"/dest/usr/${CHOST}/include "${pkgdir}"/usr/
  rm -f "${pkgdir}"/usr/include/iconv.h
  rm -f "${pkgdir}"/usr/include/unctrl.h
  # provided by libtirpc
  rm -fr "${pkgdir}"/usr/include/rpc/

  cp -rLf "${srcdir}"/dest/usr/${CHOST}/lib "${pkgdir}"/usr/
}

# return 0
# To hack on this:
# cd /c/repo-MSYS2/msys2-runtime/
# pushd src/build-i686-pc-msys
# LANG=C make && make -j1 DESTDIR=/c/repo-MSYS2/msys2-runtime/src/dest install
# popd
# makepkg -sRLf
# pacman -U msys2-runtime*.xz

# Quicker:
# open cmd.exe
# set "PATH=C:\\msys32\\usr\\bin;%PATH%"
# E:
# pushd E:\m2\repo-MSYS2\msys2-runtime\src\build-i686-pc-msys\i686-pc-msys\winsup\cygwin
# C:/msys32/usr/bin/bash -c "LANG=C && make"
# copy /y new-msys-2.0.dll C:\msys32\usr\bin\msys-2.0.dll
# popd
# C:
# C:/msys32/usr/bin/strace ls / > C:/strace.txt 2>&1
#
