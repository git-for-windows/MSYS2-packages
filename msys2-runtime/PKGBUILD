# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Ray Donnelly <mingw.android@gmail.com>

pkgbase=msys2-runtime
pkgname=('msys2-runtime' 'msys2-runtime-devel')
pkgver=3.4.6
pkgrel=1
pkgdesc="Cygwin POSIX emulation engine"
arch=('x86_64')
url="https://www.cygwin.com/"
license=('GPL')
groups=('base')
makedepends=('cocom'
             'texinfo'
             'git'
             'perl'
             'gcc'
             'make'
             'mingw-w64-cross-crt-git'
             'mingw-w64-cross-gcc'
             'mingw-w64-cross-zlib'
             'zlib-devel'
             'gettext-devel'
             'libiconv-devel'
             'autotools'
             'xmlto'
             'docbook-xsl')
source=('msys2-runtime'::git+https://github.com/cygwin/cygwin#tag=cygwin-${pkgver}
        msys2-runtime.commit
        0001-Add-MSYS2-triplet.patch
        0002-Fix-msys-library-name-in-import-libraries.patch
        0003-Rename-dll-from-cygwin-to-msys.patch
        0004-Add-functionality-for-converting-UNIX-paths-in-argum.patch
        0005-Add-functionality-for-changing-OS-name-via-MSYSTEM-e.patch
        0006-Move-root-to-usr.-Change-sorting-mount-points.-By-de.patch
        0007-Instead-of-creating-Cygwin-symlinks-use-deep-copy-by.patch
        0008-Automatically-rewrite-TERM-msys-to-TERM-cygwin.patch
        0009-Do-not-convert-environment-for-strace.patch
        0010-path_conv-special-case-root-directory-to-have-traili.patch
        0011-dcrt0.cc-Untangle-allow_glob-from-winshell.patch
        0012-dcrt0.cc-globify-Don-t-quote-literal-strings-differe.patch
        0013-strace.cc-Don-t-set-MSYS-noglob.patch
        0014-Add-debugging-for-build_argv.patch
        0015-Add-debugging-for-strace-make_command_line.patch
        0016-environ.cc-New-facility-environment-variable-MSYS2_E.patch
        0017-Fix-native-symbolic-link-spawn-passing-wrong-arg0.patch
        0018-strace-quiet-be-really-quiet.patch
        0019-Introduce-the-enable_pcon-value-for-MSYS.patch
        0020-popen-call-usr-bin-sh-instead-of-bin-sh.patch
        0021-Expose-full-command-lines-to-other-Win32-processes-b.patch
        0022-Do-not-show-Error-dialogs-by-default.patch
        0023-Add-a-helper-to-obtain-a-function-s-address-in-kerne.patch
        0024-Emulate-GenerateConsoleCtrlEvent-upon-Ctrl-C.patch
        0025-kill-kill-Win32-processes-more-gently.patch
        0026-Cygwin-make-option-for-native-inner-link-handling.patch
        0027-docs-skip-building-texinfo-and-PDF-files.patch
        0028-install-libs-depend-on-the-toollibs.patch
        0029-POSIX-ify-the-SHELL-variable.patch
        0030-Handle-ORIGINAL_PATH-just-like-PATH.patch
        0031-uname-allow-setting-the-system-name-to-CYGWIN.patch
        0032-Pass-environment-variables-with-empty-values.patch
        0033-Optionally-disallow-empty-environment-values-again.patch
        0034-build_env-respect-the-MSYS-environment-variable.patch
        0035-When-converting-to-a-Unix-path-avoid-double-trailing.patch
        0036-msys2_path_conv-pass-PC_NOFULL-to-path_conv.patch
        0037-Revert-Cygwin-Enable-dynamicbase-on-the-Cygwin-DLL-b.patch
        0038-dumper-avoid-linker-problem-when-libbfd-depends-on-l.patch
        0039-Stop-assuming-that-there-are-no-spaces-in-POSIX-styl.patch
        0040-Handle-8-bit-characters-under-LOCALE-C.patch
        0041-Mention-the-extremely-useful-small_printf-function.patch
        0042-Leave-arguments-starting-with-a-tilde-or-quote-alone.patch
        0043-Leave-Git-s-name-and-message-arguments-alone-please.patch
        0044-Leave-paths-containing-any-special-characters-alone.patch
        0045-Leave-paths-containing-alone.patch
        0046-Skip-posix-to-windows-conversion-when-is-seen.patch
        0047-Also-leave-Git-s-rev-.-name-syntax-alone.patch
        0048-Arguments-starting-with-are-no-paths.patch
        0049-Prevent-scp-style-arguments-from-being-mangled.patch
        0050-Fixed-path-converting-with-non-ascii-char.patch
        0051-path-conversion-Introduce-ability-to-switch-off-conv.patch
        0052-Allow-overriding-the-home-directory-via-the-HOME-var.patch
        0053-Respect-db_home-setting-even-for-the-SYSTEM-account.patch
        0054-Respect-the-db_home-env-setting-under-more-circumsta.patch
        0055-Allow-native-symlinks-to-non-existing-targets-in-nat.patch
        0056-Make-paths-WCS-MBS-conversion-explicit.patch
        0057-Use-MB_CUR_MAX-6-by-default.patch
        0058-Change-the-default-base-address-for-x86_64.patch
        0059-Avoid-sharing-cygheaps-across-Cygwin-versions.patch
        0060-uname-report-msys2-runtime-commit-hash-too.patch)
sha256sums=('SKIP'
            '67751e4d22f6e80eedde68c7d63fcbc4b8d43a6f61728ea4105908d7e8787a3a'
            'e061d933f7ec1a6ba5a8c56cbdb9e796e3df266409893afba33d9f4f5365f000'
            '52f3b36d3e420374b75af3502afb971279b70828a88fe05870fe35ccab90e2f0'
            'd1526bdea50b470fbbf5a6d5d5d7814a1bed7940d380e140153c290ead320979'
            'e8daf8e0db5d461ab8123ceabc3c7bb68745bb3ea423662c1143c04f7c15bf9f'
            'f09fc1f524b0030217840b3ee0052b1a0dfdefb28f853a892e0a945493a77675'
            'b3f2cc6e8ae2844328d45f1dcb52726727136bb5f333d91d546c9ee191521f46'
            '7b8947971fafec540d0cbb87334c4908c0ab22d857163f39f3306598f7710fd1'
            '857ca18800f6001f147375339396ed7fffca234a2ebcff5f3ab40ed70d003a14'
            '168d9b93d8c65892d4e5a87e0c71756c529bb94b94f4d65d67cc6622e1a23b13'
            '5ff8300f3085e2441059afc44ce4b15068cc34f2e1f09c31520ffdd372201165'
            '7d8e0c9cb242311fb6a6357c79d05302a073c6466391503fdb89f1551958978f'
            '771c3440a2567b16bade9d2970d7aab3aa8ab4b39f59da532b338642b06bfb46'
            'a43a5f9f99aac2e86ee6059f21d00d032ac80c58ee93918faf859e383b70e362'
            '09643102d1c24cac95c88d8a6e8f6c5d252d81856c4aef79baa7952d7a676d2f'
            'fbb923ce97db0c64471190dfa5d6a3930a363b5b55df2f44fae3ce98ce2a7243'
            '8b8713c82ad5de58256b883f694d1a224dde0d30a52eeb22f7a2783c000935c6'
            'aca7ae31c6c77f825d5259ca7c47df83580cbbc4dff10532c6446d7e4bab1d4a'
            'c8a0b212ee8613c5637a6cf3c30b0b0bd2ff49368226cbacaf540b0746bd6b9d'
            '3505c35fa45eeca6a18a336c54275ba43bb265b5a96443c99a93038e8747e541'
            '5dbde5d4f364faaf8db2e41f8443bd4b57ebef2b9e306ea48ced3128c83180e4'
            'e4869bdb6179637f323114ea208d816e2c56563a803428c7fadf1015e90252a1'
            'a4558b81aab181cfad0c581d48b269e265ecd5f0dc106e0962e9c3e34408fa4f'
            'fc01b33d011e04a6b8f1b9c0d08aaeac9376ed0b442033afe631b42201658877'
            'dd5d5d15f7cadf12c8473b69e5819e5521be915525f2852ab5e1e0a5b3add8fa'
            '51d946cef11b6242f79e21301bc98ca609a54a7dad1ed978751958c4f1949c81'
            '45f81ef15a35e6902c07bea120daca7de5380b010abaface7fbdd2957debb63e'
            'd2ffdcb224ac8a03644b562370007ebafdca149d200f620dd1012ff64df138ec'
            '3879d7895a79c230b544a6803677c54e7d170601af20b21e39366abdd2b979d9'
            'e596cca83c3693f88a792450cebd98c91aa5acceb5dbc123d7dc8953b77b4253'
            '1e8e97b3fb7e44225b41da7a23f2d0ceb15c4284bf58c62b650f01b4c088df92'
            '236755453a0538687c3cde6d90db4b8605bc11d8df6274a7ffd0a7fcb77a6434'
            '56df2a514a2c4d179ad4d5a26e752793b729de1d73d4cfd89a4b16bbe4191bb9'
            '239145da9a1b1e5a8203b4aab7b6bbd8100c90e976c6340fd8239432ce7eae1d'
            '381232e5d08ef918df245036a956c0cb2cf764d1ff3033bcd4f8b3e66e256a16'
            '70b2ede5a2c01b68efb25d3907257a10fad1134a98f6d746f3f907e4c2a3df0c'
            '1965d4f68d3469812fea98f8ff33618a1340b5a4da808228c4e3c4a606e70a49'
            '0e5eb6c16e10adcf85c86478614dcaace23ea4a85d6b5dab404b8973c0abd7c7'
            'a6364d50b4afdd27e54708f6475588d9146885e0d27c3fe2cbfcb0710958f2df'
            '82692ce01d0ff92b265cbb6803cdcfb1d6613587d0b73521776156da3e4be411'
            '2b62c321cdf673fb38fdcf442eaf7cdd3783ceb3d7100f53d5ec7dbe7516bc27'
            'ecadc424bee57a0a3de275cea1682814d9d4e80cbdb0877fff7a2a76598a0d15'
            '52af1951ae0fb9d9eb3ea457d0fae15e9dd6f164c7a1633f79107d0b579e4565'
            'fd79bc01256bd0f5ab7c6660f78f110efb18d71ef34b17c45a9bf13dede42541'
            'c2399b0378ca01fa741a8187afb306855b4e35623a0e996bbfba7915a7394fd7'
            '150d89cb11ee8ff0167d06ac84a2e25ce8939341f2b8031fcfccf34a0e1f7124'
            'f5ce9d9bd4e8312e58abf3708bd8f200270881334fce9106e42330fee5e36edc'
            'a1ffa33c930b9a0b505c5e1004d8b5e973029129195d8b5f2c4ebd7659525963'
            '2400a19059ff932db89c1475876ad2d59a1f423d497f4e4a65b45de00fa43b48'
            '144c29a963a93ae20a3fde79b4c96824f122d11617534551a6b3efb25cad14d9'
            'a7960077c3d47ec76f94e0f38d775f92d5777385a1320121c3fcc014a0caabbe'
            'c52518df7fe713a5bb0745cb7a308f4891f998ba9f5c4d492e43075a25bf7514'
            '4c51563273a30f1ca57a6d10fc34da985af75de6fd8dfd432ef3321fd762469c'
            'b81376d2f8a1f29b750efcc2dc9ef4d531c91f6ec1394fde7b3e67fa3d1a2ab3'
            '23c75cf56469b76a366bfcdcffd22ed83b045ff66dfe930df3cfd65d2872b5b7'
            'aa125ce529b357f5f928679ad23467d63ecc2f4484c11b25ca0d58db42726a18'
            '6956726bd69e0bab034442c5191b3da402cefadfbf4a101993341f508e812935'
            '0dd0b9dc3d24e6556c5f6c758a3157405ae2a75b3760fab92acc3b183151bb50'
            '6e6c217266559b46cd41e60b6cbe8f87a6166c7ea51d8c07241974c504c9ec15'
            '7512fc55bbb0bc6c111ce05aa90269eb4423de8092a063be4009404967b1c6bd'
            '77f4b172f0e3aee99e1c2da966efffa2e5440ef91e8374a53c488923baac1751')

# Helper macros to help make tasks easier #
apply_patch_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying $_patch"
    patch -Nbp1 -i "${srcdir}/${_patch}"
  done
}

apply_git_am_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying $_patch"
    git apply "${srcdir}/${_patch}"
  done
}

del_file_exists() {
  for _fname in "$@"
  do
    if [ -f $_fname ]; then
      rm -rf $_fname
    fi
  done
}
# =========================================== #

prepare() {
  cd "${srcdir}"/msys2-runtime
  if test true != "$(git config core.symlinks)"
  then
    git config core.symlinks true &&
    /usr/bin/git reset --hard
  fi
  if test false != "$(git config core.autocrlf)"
  then
    git config core.autocrlf false &&
    rm "$(git rev-parse --git-path index)" &&
    /usr/bin/git reset --hard
  fi
  del_file_exists winsup/cygwin/msys2_path_conv.cc \
    winsup/cygwin/msys2_path_conv.h
 apply_git_am_with_msg 0001-Add-MSYS2-triplet.patch \
  0002-Fix-msys-library-name-in-import-libraries.patch \
  0003-Rename-dll-from-cygwin-to-msys.patch \
  0004-Add-functionality-for-converting-UNIX-paths-in-argum.patch \
  0005-Add-functionality-for-changing-OS-name-via-MSYSTEM-e.patch \
  0006-Move-root-to-usr.-Change-sorting-mount-points.-By-de.patch \
  0007-Instead-of-creating-Cygwin-symlinks-use-deep-copy-by.patch \
  0008-Automatically-rewrite-TERM-msys-to-TERM-cygwin.patch \
  0009-Do-not-convert-environment-for-strace.patch \
  0010-path_conv-special-case-root-directory-to-have-traili.patch \
  0011-dcrt0.cc-Untangle-allow_glob-from-winshell.patch \
  0012-dcrt0.cc-globify-Don-t-quote-literal-strings-differe.patch \
  0013-strace.cc-Don-t-set-MSYS-noglob.patch \
  0014-Add-debugging-for-build_argv.patch \
  0015-Add-debugging-for-strace-make_command_line.patch \
  0016-environ.cc-New-facility-environment-variable-MSYS2_E.patch \
  0017-Fix-native-symbolic-link-spawn-passing-wrong-arg0.patch \
  0018-strace-quiet-be-really-quiet.patch \
  0019-Introduce-the-enable_pcon-value-for-MSYS.patch \
  0020-popen-call-usr-bin-sh-instead-of-bin-sh.patch \
  0021-Expose-full-command-lines-to-other-Win32-processes-b.patch \
  0022-Do-not-show-Error-dialogs-by-default.patch \
  0023-Add-a-helper-to-obtain-a-function-s-address-in-kerne.patch \
  0024-Emulate-GenerateConsoleCtrlEvent-upon-Ctrl-C.patch \
  0025-kill-kill-Win32-processes-more-gently.patch \
  0026-Cygwin-make-option-for-native-inner-link-handling.patch \
  0027-docs-skip-building-texinfo-and-PDF-files.patch \
  0028-install-libs-depend-on-the-toollibs.patch \
  0029-POSIX-ify-the-SHELL-variable.patch \
  0030-Handle-ORIGINAL_PATH-just-like-PATH.patch \
  0031-uname-allow-setting-the-system-name-to-CYGWIN.patch \
  0032-Pass-environment-variables-with-empty-values.patch \
  0033-Optionally-disallow-empty-environment-values-again.patch \
  0034-build_env-respect-the-MSYS-environment-variable.patch \
  0035-When-converting-to-a-Unix-path-avoid-double-trailing.patch \
  0036-msys2_path_conv-pass-PC_NOFULL-to-path_conv.patch \
  0037-Revert-Cygwin-Enable-dynamicbase-on-the-Cygwin-DLL-b.patch \
  0038-dumper-avoid-linker-problem-when-libbfd-depends-on-l.patch \
  0039-Stop-assuming-that-there-are-no-spaces-in-POSIX-styl.patch \
  0040-Handle-8-bit-characters-under-LOCALE-C.patch \
  0041-Mention-the-extremely-useful-small_printf-function.patch \
  0042-Leave-arguments-starting-with-a-tilde-or-quote-alone.patch \
  0043-Leave-Git-s-name-and-message-arguments-alone-please.patch \
  0044-Leave-paths-containing-any-special-characters-alone.patch \
  0045-Leave-paths-containing-alone.patch \
  0046-Skip-posix-to-windows-conversion-when-is-seen.patch \
  0047-Also-leave-Git-s-rev-.-name-syntax-alone.patch \
  0048-Arguments-starting-with-are-no-paths.patch \
  0049-Prevent-scp-style-arguments-from-being-mangled.patch \
  0050-Fixed-path-converting-with-non-ascii-char.patch \
  0051-path-conversion-Introduce-ability-to-switch-off-conv.patch \
  0052-Allow-overriding-the-home-directory-via-the-HOME-var.patch \
  0053-Respect-db_home-setting-even-for-the-SYSTEM-account.patch \
  0054-Respect-the-db_home-env-setting-under-more-circumsta.patch \
  0055-Allow-native-symlinks-to-non-existing-targets-in-nat.patch \
  0056-Make-paths-WCS-MBS-conversion-explicit.patch \
  0057-Use-MB_CUR_MAX-6-by-default.patch \
  0058-Change-the-default-base-address-for-x86_64.patch \
  0059-Avoid-sharing-cygheaps-across-Cygwin-versions.patch \
  0060-uname-report-msys2-runtime-commit-hash-too.patch
}

build() {
  [[ -d "${srcdir}"/build-${CHOST} ]] && rm -rf "${srcdir}"/build-${CHOST}
  mkdir -p "${srcdir}"/build-${CHOST} && cd "${srcdir}"/build-${CHOST}

  # Gives more verbose compile output when debugging.
  local -a extra_config
  if check_option "debug" "y"; then
    export CCWRAP_VERBOSE=1
    OPTIM="-O0"
    extra_config+=(--enable-debugging)
  else
    OPTIM="-O2"
  fi

  CFLAGS="$OPTIM -pipe -ggdb -Wno-error=deprecated -Wno-error=stringop-truncation -Wno-error=missing-attributes -Wno-error=maybe-uninitialized" #-Wno-error=class-memaccess
  CXXFLAGS="$OPTIM -pipe -ggdb -Wno-error=deprecated -Wno-error=stringop-truncation -Wno-error=missing-attributes -Wno-error=maybe-uninitialized" #-Wno-error=class-memaccess

  # otherwise it asks git which appends "-dirty" because of our uncommited patches
  CFLAGS+=" -DCYGPORT_RELEASE_INFO=${pkgver}"

  (cd "${srcdir}/msys2-runtime/winsup" && ./autogen.sh)

  "${srcdir}"/msys2-runtime/configure \
    --with-msys2-runtime-commit="$(cat "${srcdir}/msys2-runtime.commit")" \
    --prefix=/usr \
    --build=${CHOST} \
    --sysconfdir=/etc \
    "${extra_config[@]}"
  LC_ALL=C make

  if test -n "$SIGNTOOL"
  then
    eval "$SIGNTOOL" \
      ${PWD}/${CHOST}/winsup/cygwin/new-msys-2.0.dll \
      ${PWD}/${CHOST}/winsup/utils/*.exe ${PWD}/${CHOST}/winsup/utils/mingw/*.exe
  fi

  LC_ALL=C make -j1 DESTDIR="${srcdir}"/dest install

  #pushd ${CHOST}/winsup/cygwin > /dev/null
  #LANG=C make libmsys2_s.a
  #cp libmsys2_s.a "${srcdir}"/dest/usr/${CHOST}/lib/
  #popd > /dev/null

  rm -rf "${srcdir}"/dest/etc
}

package_msys2-runtime() {
  pkgdesc="Posix emulation engine for Windows"
  groups=('base')
  conflicts=('catgets' 'libcatgets' 'msys2-runtime-3.4')
  replaces=('catgets' 'libcatgets' 'msys2-runtime-3.4')

  mkdir -p "${pkgdir}"/usr
  cp -rf "${srcdir}"/dest/usr/bin "${pkgdir}"/usr/
  cp -rf "${srcdir}"/dest/usr/libexec "${pkgdir}"/usr/
  rm -f "${pkgdir}"/usr/bin/cyglsa-config
  rm -f "${pkgdir}"/usr/bin/cyglsa.dll
  rm -f "${pkgdir}"/usr/bin/cyglsa64.dll
  rm -f "${pkgdir}"/usr/bin/cygserver-config
  cp -rf "${srcdir}"/dest/usr/share "${pkgdir}"/usr/
}

package_msys2-runtime-devel() {
  pkgdesc="MSYS2 headers and libraries"
  groups=('msys2-devel')
  depends=("msys2-runtime=${pkgver}")
  conflicts=('libcatgets-devel' 'msys2-runtime-3.4-devel')
  replaces=('libcatgets-devel' 'msys2-runtime-3.4-devel')

  mkdir -p "${pkgdir}"/usr/bin
  cp -rLf "${srcdir}"/dest/usr/${CHOST}/include "${pkgdir}"/usr/
  rm -f "${pkgdir}"/usr/include/iconv.h
  rm -f "${pkgdir}"/usr/include/unctrl.h
  # provided by libtirpc
  rm -fr "${pkgdir}"/usr/include/rpc/

  cp -rLf "${srcdir}"/dest/usr/${CHOST}/lib "${pkgdir}"/usr/
}

# return 0
# To hack on this:
# cd /c/repo-MSYS2/msys2-runtime/
# pushd src/build-i686-pc-msys
# LANG=C make && make -j1 DESTDIR=/c/repo-MSYS2/msys2-runtime/src/dest install
# popd
# makepkg -sRLf
# pacman -U msys2-runtime*.xz

# Quicker:
# open cmd.exe
# set "PATH=C:\\msys32\\usr\\bin;%PATH%"
# E:
# pushd E:\m2\repo-MSYS2\msys2-runtime\src\build-i686-pc-msys\i686-pc-msys\winsup\cygwin
# C:/msys32/usr/bin/bash -c "LANG=C && make"
# copy /y new-msys-2.0.dll C:\msys32\usr\bin\msys-2.0.dll
# popd
# C:
# C:/msys32/usr/bin/strace ls / > C:/strace.txt 2>&1
#
