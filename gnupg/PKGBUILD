# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=gnupg
pkgver=2.3.8
pkgrel=1
pkgdesc='Complete and free implementation of the OpenPGP standard'
provides=('dirmngr' "gnupg2=${pkgver}")
url='https://gnupg.org/'
license=('GPL')
arch=('i686' 'x86_64')
makedepends=('gettext-devel'
             'libassuan-devel'
             'libbz2-devel'
             'libcurl-devel'
             'libgcrypt-devel'
             'libgnutls-devel'
             'libgpg-error-devel'
             'libiconv-devel'
             'libksba-devel'
             'libnettle-devel'
             'libnpth-devel'
             'libp11-kit-devel'
             'libreadline-devel'
             'libsqlite-devel'
             'libtasn1-devel'
             'libunistring-devel'
             'nettle'
             'tar'
             'zlib-devel')
optdepends=('curl: gpg2keys_curl')
depends=('bzip2'
         'libassuan'
         'libbz2'
         'libcurl'
         'libgcrypt'
         'libgpg-error'
         'libgnutls'
         'libiconv'
         'libintl'
         'libksba'
         'libnpth'
         'libreadline'
         'libsqlite'
         'nettle'
         'pinentry'
         'zlib'
        )
source=("https://gnupg.org/ftp/gcrypt/${pkgname}/${pkgname}-${pkgver}.tar.bz2"{,.sig}
        '0001-avoid-beta-warning.patch'
        '0002-gnupg-2.3.8-msys2.patch'
        '0003-dirmngr-Fix-build-with-no-LDAP-support.patch'
        '0004-gnupg-2.2.9-have-drive-letters.patch'
        '0005-Skip-building-the-.pdf-and-info-files.patch')
sha256sums=('540b7a40e57da261fb10ef521a282e0021532a80fd023e75fb71757e8a4969ed'
            'SKIP'
            'a7039425b3b4c515048caf9af79be7cdab161cbcb99ea6e086f01cf9c5fde6f3'
            '441d93f035b7f1789300fd7ae413dd5211619a06e54375b3a8acc9d721bef5be'
            '881ec4f04d6450535e095a7a31ef239d0760204bcc182326171ae88c1c3a534d'
            'e3cbb5fe8a669b4e1be67c1c08d18da14b129f443eafbc659db07fd290adc540'
            'fe1216d048a7fd5ebc522fbcab0b7e4b993804601f175bc13eae3e0e4b380818')
validpgpkeys=('031EC2536E580D8EA286A9F22071B08A33BD3F06'
              'D8692123C4065DEA5E0F3AB5249B39D24F25E3B6'
              '6DAA6E64A76D2840571B4902528897B826403ADA')
install=${pkgname}.install

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  sed '/noinst_SCRIPTS = gpg-zip/c sbin_SCRIPTS += gpg-zip' -i tools/Makefile.in
  patch -p1 -i ${srcdir}/0001-avoid-beta-warning.patch
  patch -p1 -i ${srcdir}/0002-gnupg-2.3.8-msys2.patch
  patch -p1 -i ${srcdir}/0003-dirmngr-Fix-build-with-no-LDAP-support.patch
  patch -p1 -i ${srcdir}/0004-gnupg-2.2.9-have-drive-letters.patch
  patch -p1 -i ${srcdir}/0005-Skip-building-the-.pdf-and-info-files.patch

  autoreconf -fiv

  sed 's/development_version=yes/development_version=no/' -i configure
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure \
    --build=${CHOST} \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --sbindir=/usr/bin \
    --libexecdir=/usr/lib/gnupg \
    --disable-libdns \
    --enable-maintainer-mode

  make
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make check
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
