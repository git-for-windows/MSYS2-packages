# Maintainer: Alexey Pavlov <Alexpux@gmail.com>

pkgbase=libgcrypt
pkgname=('libgcrypt' 'libgcrypt-devel')
pkgver=1.9.3
pkgrel=2
pkgdesc="General purpose cryptographic library based on the code from GnuPG"
arch=(i686 x86_64)
url="https://gnupg.org"
license=('LGPL')
depends=('libgpg-error')
makedepends=('libgpg-error-devel')
options=('!emptydirs')
source=(https://gnupg.org/ftp/gcrypt/${pkgname}/${pkgname}-${pkgver}.tar.bz2
        0001-libgcrypt-1.7.3-ecc-test-fix.patch
        0002-libgcrypt-1.8.0-use-poll.patch
        0003-libgcrypt-1.6.1-msys2.patch
        0004-fix-broken-mlock-detection.patch)
sha256sums=('97ebe4f94e2f7e35b752194ce15a0f3c66324e0ff6af26659bbfb5ff2ec328fd'
            '13d650b9383b728e8e8e5e0cd94121b8718018d56aff1d605d6180a367b5395c'
            'ae324a1661907adc38e158fb43e9de99036f539bc0ce57434b60bfb841a1f10e'
            '4b496b2bbc592a8549cde869938ab7d99894622cb12746b930a6315aec336821'
            '33deefac521eee218b844d4f575a78737f34a32c67a4cf30fc37f6da0ea11e47')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  patch -p1 -i ${srcdir}/0001-libgcrypt-1.7.3-ecc-test-fix.patch
  patch -p1 -i ${srcdir}/0002-libgcrypt-1.8.0-use-poll.patch
  patch -p1 -i ${srcdir}/0003-libgcrypt-1.6.1-msys2.patch
  patch -p1 -i ${srcdir}/0004-fix-broken-mlock-detection.patch

  autoreconf -fi
}

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  ./configure \
    --prefix=/usr \
    --build=${CHOST} \
    --enable-shared \
    --enable-static \
    --exec-prefix=/usr \
    --localstatedir=/var \
    --sysconfdir=/etc \
    --docdir=/usr/share/doc/libgcrypt \
    --htmldir=/usr/share/doc/libgcrypt/html \
    --config-cache \
    --disable-asm

  make no_undefined=-no-undefined
  make DESTDIR=${srcdir}/dest install
}

check() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make check
}

package_libgcrypt() {
  groups=('libraries')
  install=${pkgname}.install

  mkdir -p ${pkgdir}/usr/share
  cp -rf ${srcdir}/dest/usr/bin ${pkgdir}/usr/
  rm -f ${pkgdir}/usr/bin/*-config
  cp -rf ${srcdir}/dest/usr/share/info ${pkgdir}/usr/share/
}

package_libgcrypt-devel() {
  pkgdesc="Libgcrypt headers and libraries"
  groups=('development')
  options=('staticlibs')
  depends=("libgcrypt=${pkgver}" 'libgpg-error-devel')

  mkdir -p ${pkgdir}/usr/{bin,share}
  cp -f ${srcdir}/dest/usr/bin/*-config ${pkgdir}/usr/bin/
  cp -rf ${srcdir}/dest/usr/include ${pkgdir}/usr/
  cp -rf ${srcdir}/dest/usr/lib ${pkgdir}/usr/
  cp -rf ${srcdir}/dest/usr/share/aclocal ${pkgdir}/usr/share/
}
